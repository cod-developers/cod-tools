#!/bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl5 -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------
#*
# Convert a COD XRF (eXtended ReFerence) format into a CIF file.
#**

use strict;

my @processed_tags = (
    "_cell_measurement_temperature",
    "_diffrn_ambient_temperature",
    ## "_symmetry_equiv_pos_as_xyz",
);

my %tags = ();
my @record = ();
my @anisou = ();
my %anisou = ();

my $n = 0;
my $compound = 1;

while(<>) {
    chomp;
    if( /^\#/ ) {
	my $idx;
	my ( $key, $value ) = split( /[\s]/, $_, 2 );
	$key =~ s/^\#//;
	if( $key =~ /^(.*)\[(.*)\]$/ ) {
	    $key = $1;
	    $idx = $2;
	}
	if( defined $idx ) {
	    if( !defined $tags{$key}[$idx] ) {
		$tags{$key}[$idx] = $value;
	    } else {
		$tags{$key}[$idx] .= "\n" . $value;
	    }
	} else {
	    if( !defined $tags{$key} ) {
		$tags{$key} = $value;
	    } else {
		$tags{$key} .= "\n" . $value;
	    }
	}
    } elsif( /^ANISOU/ ) {
	my @atom = split( " ",$_ );
	my $atom_name = $atom[1];
	push( @anisou, [ @atom ] );
	$anisou{$atom_name} ++;
    } elsif( !/^\s*$/ ) {
	if( $n < 4 && $n != 2 ) {
	    push( @record, [$_] );
	} else {
	    push( @record, [ split( " ", $_ ) ] );
	}
	$n++;
    }
    
    if( /^\s*$/ || eof ) {
	print_record( \@record, \%tags, \@anisou, \%anisou );
	@record = ();
	%tags = ();
	@anisou = ();
	%anisou = ();
	$n = 0;
    }
}

sub print_tag
{
    my ($key, $tags) = @_;

    if( exists $tags->{$key} ) {
	my $val = $tags->{$key};
	if( ref $val eq "ARRAY" ) {
	    print "loop_\n";
	    print "$key\n";
	    for my $value (@$val) {
		print_value( $value );
	    }
	} else {
	    printf "%-30s", $key;
	    print_value( $val );
	}
    }
}

sub print_value
{
    my $val = $_[0];

    if( $val =~ /\n/ ) {
	$val = "\n; " . $val . "\n;\n";
    } elsif( $val =~ /^[A-Za-z0-9()]*$/) {
	$val = " " . $val . "\n";
    } else {
	$val = " '" . $val . "'\n";
    }
    print $val;
}

sub print_record
{
    my ($records, $tags, $anisou, $anisou_atoms ) = @_;

    printf "data_%d\n", 7000000 + $compound++;

    ## do { local $, = ",\n"; print keys %{$tags}, "\n"; };

    if( $tags ) {
	for my $key (@processed_tags) {
	    if( exists $tags->{$key} ) {
		my $val = $tags->{$key};
		if( ref $val eq "ARRAY" ) {
		    print "loop_\n";
		    print "$key\n";
		    for my $value (@$val) {
			print_value( $value );
		    }
		} else {
		    printf "%-30s", $key;
		    print_value( $val );
		}
	    }
	}
    }

    if( $records && int(@{$records}) > 0 ) {
	my $bibliography = shift( @{$records} );
	my $composition =  shift( @{$records} );
	my $cell =  shift( @{$records} );
	my $spacegroup = shift( @{$records} );
	print "_publ_section_title \n";
	print ";\n";
	do {
	    local $, = "\n";
	    local $\ = "\n";
	    print map { " " . $_ } fold( 71, " ", ($bibliography->[0]) );
	};
	print ";\n";
	print "_chemical_formula_sum '$composition->[0]'\n";
	print "_cell_length_a       $cell->[0]\n";
	print "_cell_length_b       $cell->[1]\n";
	print "_cell_length_c       $cell->[2]\n";
	print "_cell_angle_alpha    $cell->[3]\n";
	print "_cell_angle_beta     $cell->[4]\n";
	print "_cell_angle_gamma    $cell->[5]\n";
	print "_symmetry_space_group_name_H-M   ";
	print_value( $spacegroup->[0] );
	print_tag( "_symmetry_equiv_pos_as_xyz", $tags );
	if( int(@{$records}) > 0 ) {
	    print "loop_\n";
	    print "_atom_site_label\n";
	    print "_atom_site_symmetry_multiplicity\n";
	    print "_atom_site_Wyckoff_symbol\n";
	    print "_atom_site_fract_x\n";
	    print "_atom_site_fract_y\n";
	    print "_atom_site_fract_z\n";
	    print "_atom_site_occupancy\n";
	    print "_atom_site_U_iso_or_equiv\n";
	    print "_atom_site_adp_type\n";
	    local $, = " ";
	    local $\ = "\n";
	    for my $atom (@{$records}) {
		my $atom_name = $atom->[0];
		print @{$atom}, defined $anisou_atoms->{$atom_name} ?
		    "Uani" : "Uiso";
	    }
	}
    }

    if( $anisou && int(@{$anisou}) > 0 ) {
	print "loop_\n";
	print "_atom_site_aniso_label\n";
	print "_atom_site_aniso_U_11\n";
	print "_atom_site_aniso_U_22\n";
	print "_atom_site_aniso_U_33\n";
	print "_atom_site_aniso_U_23\n";
	print "_atom_site_aniso_U_13\n";
	print "_atom_site_aniso_U_12\n";
	local $, = " ";
	local $\ = "\n";
	for my $atom (@{$anisou}) {
	    print @{$atom}[1..$#{$atom}];
	}
    }

    print "\n";
}

sub fold
{
    my $length = shift;
    my $separator = shift;
    my $string = shift;
    my @lines = ();
    my $line = "";

    my $ors = $separator =~ /\s/ ? $separator : "$separator ";
    my $word;
    for $word (split( $separator, $string )) {
	$word =~ s/^\s*|\s*$//g;
	if( !$line ) {
	    $line = $word;
	} else {
	    my $new_line = "$line$ors$word";
	    if( length($new_line) < $length ) {
		$line = $new_line;
	    } else {
		push( @lines, $line );
		$line = $word;
	    }
	}
    }
    push( @lines, $line );
    return @lines;
}

#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Correct temperature values which have units specified or convert 
#  between Celsius degrees and Kelvins. Changes 'room/ambiante temperature'  
#  to the appropriate numeric value. Fixes other undefined values('none', 
#  'not measured', etc.) to '?' simbol.
#  Determine a report about changes made into standart I/O streams.
#**

use strict;
use lib "./lib/perl5";
use lib "./CIFParser";
use CIFParser;
use CIFTagCanonicalNames;
use CIFDictTags;
use CIFCODTags;
use CIFTagPrint;
use CIFTagManage;
use SOptions;
use SUsage;

my $Id = '$Id$';
my $keep_tag_order = 0;

@ARGV = getOptions(
    "--keep-tag-order" => sub { $keep_tag_order = 1; },
    "--sort-tags"      => sub { $keep_tag_order = 0; },
    "--help,--usage"   => sub { SUsage::usage; exit },
);

#
# Subroutines:
#

sub insert_report_to_comments {
    my ($dataset, $insert_reports) = @_;
    if ( @$insert_reports > 0 ) {
        my $comments_tag = '_cod_depositor_comments';
        my $values = $dataset->{values};
        my $reports_value = join("\n\n",@$insert_reports);
        
        if( exists $values->{$comments_tag} ) {
            $values->{$comments_tag}[0] = (
                $values->{$comments_tag}[0] . "\n\n" . 
                join( "\n", map { "" . $_ }
                    CIFTagPrint::fold( 70, " +", " ", $reports_value ))
            );
        } else {
            $values->{$comments_tag}[0] = ( 
                "\n" . join( "\n", map { "" . $_ }
                    CIFTagPrint::fold( 70, " +", " ", $reports_value )) 
            );
          }
        my $signature = $Id;
        $signature =~ s/^\$|\$$//g;
        #$values->{$comments_tag}[0] .= "\n\n" . $signature;
    }
} 

my @insert_reports = ();
my @dictionary_tags = ( @CIFDictTags::tag_list, @CIFCODTags::tag_list );
my %dictionary_tags = map { $_, $_ } @dictionary_tags;

@ARGV = ("-") unless @ARGV;

for my $filename (@ARGV) {
    my $parser = new CIFParser;
    my $data = $parser->Run($filename);

    if( defined $parser->YYData->{ERRCOUNT} &&
        $parser->YYData->{ERRCOUNT} > 0 ) {
	print STDERR "$0: $filename: ",
	$parser->YYData->{ERRCOUNT},
	" error(s) encountered while parsing CIF data\n";
	## exit -1;
	next;
    }

    canonicalize_all_names( $data );
    for my $dataset (@$data) {
    # print keys %{ $dataset->{values} };
    my $values = $dataset->{values};
    
        my @temp_tags =
            ( '_cell_measurement_temperature',		
              '_chemical_temperature_decomposition',		
              '_chemical_temperature_sublimation',
              '_diffrn_ambient_temperature',  
              '_exptl_crystal_density_meas_temp',
              '_chemical_melting_point' );                     
        my $number_pos =
            '(?:\+?' . 
            '(?:\d+(?:\.\d*)?|\.\d+)' .
            '(?:[eE][-+]?\d+)?)';
        my $number_neg =  
            '(?:\-' .
            '(?:\d+(?:\.\d*)?|\.\d+)' .
            '(?:[eE][-+]?\d+)?)';
        my $temp_K  = 
            '((?i:K(?i:elvin?)?)|(?i:K))';
        my $temp_C  = 
            '( (?i:C) | (?i:Deg\.?(?:rees?)?\s*[Cc]?) |' .
            '(?:(?i:[\\\/]+o)|(?i:O)|(?:\\\%))(?:[-_\s]*)(?i:C\.?)?)';
        my $temp_RT = 
            '(?:(?:(?:(?i:room)|(?i:amb(?i:i[ae]nte?)))' .
            '\s*(?i:temp(?:erature)?)?)|(?i:rt))'; 
        my $temp_undef = 
            '(?:(?i:yes)|(?i:no(?i:ne)?)|(?i:unknown)|' .
            '(?i:not\s*((?i:measured)|(?i:determined)|(?i:recorded)))|' .
            '(?i:N\/?(?i:[DA]))|\s*|[-])';
        my $sigma = '(?:\d+\.\d+|\d+\.|\.\d+|\d+)';
        
        for my $tag (@temp_tags) {
            if( exists $values->{$tag} ) {
                for my $i (0..$#{$values->{$tag}}) {
                    my $temperature = $values->{$tag}[$i];
                    $temperature =~ s/^\s+|\s+$//g;
                    
                    if( $temperature =~ /^\.|\?$/ ) {
                        next;    
                    }
                    if( $temperature =~ 
                            /^($number_pos|$number_pos\(\d+\))$/ ) {
                        next;    
                    }
                    if( $temperature =~ 
                            /^
                                \(?($number_pos)\s*\+\s*
                                273(?:[\.\,]\d+)?\)?$
                                /x ) {
                        $values->{$tag}[$i] = $1 + 273.15;
                        my $new_val = $values->{$tag}[$i];
                        my $report_msg = 
                            "$tag value '$temperature' changed to " .
                            "'$new_val'- converted between degrees " . 
                            "Celsius(C) and Kelvins(K).";
                        push (@insert_reports, $report_msg);
                        print STDERR "$0: $filename: NOTE, $report_msg\n";
                        next;    
                    }
                    if( $temperature =~ 
                          /^
                            (\>|\<)(?:[-_\s]*)($number_pos)\(?($sigma)?\)?
                            (?:[-_\s]*)(?:$temp_C)$
                            /x ) {
                        my $sign = $1;
                        my $number = $2;
                        my $sig  = $3; 
                        my $old_tag = $tag;
                        if( $old_tag !~ /_cell_measurement_temperature/ ) {
                            if( $sign =~ /\>/ ) {
                                $values->{$tag}[$i] =
                                  CIFTagPrint::pack_precision
                                    ( $number + 273.15 , $sig );
                                  my $new_val = $values->{$tag}[$i];
                                  my $new_tag = $old_tag . "_gt";
                                  CIFTagManage::rename_tag
                                    ( $dataset, $old_tag, $new_tag );
                                  my $report_msg = 
                                    "'$old_tag' tag changed to '$new_tag' " .
                                    "since the value specified 'more than' " .
                                    "('>') a certain temperature. Value " .
                                    "'$temperature' changed to '$new_val - " .
                                    "converted between degrees " .
                                    "Celsius(C) and Kelvins(K).";
                                  push (@insert_reports, $report_msg);
                                  print STDERR "$0: $filename: NOTE, $report_msg\n";
                                  next;
                            }
                            if( $sign =~ /\</ ) {
                                  $values->{$tag}[$i] =
                                    CIFTagPrint::pack_precision
                                      ( $number + 273.15 , $sig );
                                  my $new_val = $values->{$tag}[$i];
                                  my $new_tag = $old_tag . "_lt";
                                  CIFTagManage::rename_tag
                                    ( $dataset, $old_tag, $new_tag );
                                  my $report_msg = 
                                    "'$old_tag' tag changed to '$new_tag' " .
                                    "since the value specified 'less than' " .
                                    "('<') a certain temperature. Value " .
                                    "'$temperature' changed to '$new_val - " .
                                    "converted between degrees " .
                                    "Celsius(C) and Kelvins(K).";
                                push (@insert_reports, $report_msg);
                                print STDERR "$0: $filename: NOTE, $report_msg\n";
                                next;
                            }
                        }
                        next;
                    }
                    if( $temperature =~ 
                          /^
                            (\>|\<)(?:[-_\s]*)($number_pos)\(?($sigma)?\)?
                            (?:[-_\s]*)(?:$temp_K)?$
                            /x ) {
                        my $sign = $1;
                        my $number = $2;
                        my $sig  = $3; 
                        my $old_tag = $tag;
                        if( $old_tag !~ /_cell_measurement_temperature/ ) {
                            if( $sign =~ /\>/ ) {
                                $values->{$tag}[$i] =
                                  CIFTagPrint::pack_precision
                                    ( $number , $sig );
                                  my $new_val = $values->{$tag}[$i];
                                  my $new_tag = $old_tag . "_gt";
                                  CIFTagManage::rename_tag
                                    ( $dataset, $old_tag, $new_tag );
                                  my $report_msg = 
                                    "'$old_tag' tag changed to '$new_tag' " .
                                    "since the value specified 'more than' " .
                                    "('>') a certain temperature. Value " .
                                    "'$temperature' changed to '$new_val' - " .
                                    "value should be numeric, i.e. 'FLOAT " . 
                                    "or 'INT' without temperature units.";
                                  push (@insert_reports, $report_msg);
                                  print STDERR "$0: $filename: NOTE, $report_msg\n";
                                  next;
                            }
                            if( $sign =~ /\</ ) {
                                  $values->{$tag}[$i] =
                                    CIFTagPrint::pack_precision
                                      ( $number , $sig );
                                  my $new_val = $values->{$tag}[$i];
                                  my $new_tag = $old_tag . "_lt";
                                  CIFTagManage::rename_tag
                                    ( $dataset, $old_tag, $new_tag );
                                  my $report_msg = 
                                    "'$old_tag' tag changed to '$new_tag' " .
                                    "since the value specified 'less than' " .
                                    "('<') a certain temperature. Value " .
                                    "'$temperature' changed to '$new_val' - " .
                                    "value should be numeric, i.e. 'FLOAT " . 
                                    "or 'INT' without temperature units.";
                                push (@insert_reports, $report_msg);
                                print STDERR "$0: $filename: NOTE, $report_msg\n";
                                next;
                            }
                        }
                        next;
                    }
                    if( $temperature =~ 
                        /^  
                            ($number_pos)\s*(?:\()?[\s]*(?:\+|\+\/?\-)?
                            [\s]*($sigma)(?:\))?$
                            /x ) {
                        $values->{$tag}[$i] = 
                            CIFTagPrint::pack_precision( $1, $2 );
                        my $new_val = $values->{$tag}[$i];
                        my $report_msg = 
                            "$tag value '$temperature' changed to " .
                            "'$new_val'- fixed precision.";
                        push (@insert_reports, $report_msg);
                        print STDERR "$0: $filename: NOTE, $report_msg\n";
                        next;
                    }
                    if( $temperature =~	
                        /^
                            ($number_pos)(?:\()?($sigma)?
                            (?:\))?(?:[-_\s]*)\(?$temp_K\)?$
                            /x ) {
                        $values->{$tag}[$i] = 
                            CIFTagPrint::pack_precision( $1, $2 );
                        my $new_val = $values->{$tag}[$i];
                        my $report_msg = 
                            "$tag value '$temperature' changed to " .
                            "'$new_val' - value should be numeric, i.e." . 
                            "'FLOAT or 'INT' without temperature units.";
                        push (@insert_reports, $report_msg);
                        print STDERR "$0: $filename: NOTE, $report_msg\n";
                        next;
                    } 
                    if ( $temperature =~ 
                          /^
                            ($number_pos)(?:\()?($sigma)?
                            (?:\))?(?:[-_\s]*)\(?$temp_C\)?$
                            /x ) {
                        $values->{$tag}[$i] = 
                            CIFTagPrint::pack_precision( $1 + 273.15, $2 );
                        my $new_val = $values->{$tag}[$i];
                        my $report_msg = 
                            "$tag value '$temperature' changed to ". 
                            "'$new_val' - converted between degrees " .
                            "Celsius(C) and Kelvins(K), value should be " .
                            "numeric, permitted range [0.0;+inf] " .
                            "given in Kelvins(K) without temperature units.";
                        push (@insert_reports, $report_msg);
                        print STDERR "$0: $filename: NOTE, $report_msg\n";
                        next;
                    }
                    if( $temperature =~	
                          /^
                            ($number_neg)(?:\()?($sigma)?
                            (?:\))?(?:[-_\s]*)$temp_C?$
                            /x ) {
                        $values->{$tag}[$i] = 
                            CIFTagPrint::pack_precision( $1 + 273.15, $2 ); 
                        my $new_val = $values->{$tag}[$i];
                        my $report_msg = 
                            "$tag value '$temperature' changed to '$new_val' - " .
                            "converted between degrees Celsius(C) " .
                            "and Kelvins(K), value should be numeric, " . 
                            "permitted range [0.0;+inf] " .
                            "given in Kelvins(K) without temperature units.";
                        push (@insert_reports, $report_msg);
                        print STDERR "$0: $filename: NOTE, $report_msg\n";
                        next;				
                    }
                    if( $temperature =~ 
                        /^
                            ($temp_RT)(?:[-_\s]*)$
                            /x ) {
                        $values->{$tag}[$i] = "295(2)";
                        my $report_msg = 
                            "$tag value '$temperature' changed to " .
                            "'295(2)' - took room/ambient temperature " .
                            "average value [293;298] in Kelvins(K).";
                        push (@insert_reports, $report_msg);
                        print STDERR "$0: $filename: NOTE, $report_msg\n";                            
                        next;
                    }
                    if( $temperature =~ 
                        /^
                            ($temp_undef)(?:[-_\s]*)$
                            /x ) {
                        $values->{$tag}[$i] = "?";
                        my $report_msg = 
                            "$tag value '$temperature' changed to '?' - " .
                            "value is undefined or not given.";
                        push (@insert_reports, $report_msg);
                        print STDERR "$0: $filename: NOTE, $report_msg\n";              
                        next;
                    }
                    if( $temperature =~ 
                        /^
                            ($number_pos)\s*[\-\/\:]+\s*($number_pos)
                            (?:[-_\s]*)(?:$temp_C)$
                            /x )   {
                        my $number = ($1 + $2)/2;
                        my $sig = ($2 - $1 )/2;
                        if( $2 > $1 )   {
                            $values->{$tag}[$i] = 
                              CIFTagPrint::pack_precision
                                ( $number+273.15 , $sig );
                            my $new_val = $values->{$tag}[$i];
                            my $report_msg = 
                              "$tag value '$temperature' changed to " .
                              "'$new_val' - converted between degrees " .
                              "Celsius(C) and Kelvins(K), took " .
                              "average value and fixed precision.";
                            push (@insert_reports, $report_msg);
                            print STDERR "$0: $filename: NOTE, $report_msg\n";
                            next;
                         } 
                     }
                    if( $temperature =~ 
                        /^
                            ($number_pos)\s*[\-\/\:]+\s*($number_pos)
                            (?:[-_\s]*)(?:$temp_K)?$
                            /x )   {
                        my $number = ($1 + $2)/2;
                        my $sig = ($2 - $1 )/2;
                        if( $2 > $1 )   {
                            $values->{$tag}[$i] = 
                                  CIFTagPrint::pack_precision
                                    ( $number , $sig );
                            my $new_val = $values->{$tag}[$i];
                            my $report_msg = 
                                "$tag value '$temperature' changed to " .
                                "'$new_val' - took average value " .
                                "and fixed precision.";
                            push (@insert_reports, $report_msg);
                            print STDERR "$0: $filename: NOTE, $report_msg\n";
                        next;
                        }
                    }
                    if( $temperature !~ 
                        /^
                            ($number_pos)(\($sigma\))?$
                            /x ) {
                        print STDERR  
                            "$0: $filename: WARNING, " .
                            "$tag value is '$temperature', " .
                            "it should be numeric, " .
                            "i.e.'FLOAT or 'INT', permitted range [0.0;+inf], " .
                            "given in Kelvins(K) without temperature units.\n";
                        next;
                    }
                } 
            }
        }
        insert_report_to_comments ($dataset,\@insert_reports);  
        print_cif( $dataset, {
	    exclude_misspelled_tags => 0,
	    preserve_loop_order => 1,
	    fold_long_fields => 0,
	    dictionary_tags => \%dictionary_tags,
	    dictionary_tag_list => \@dictionary_tags,
	    keep_tag_order => $keep_tag_order,
        });
    }
}

#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Restores molecules from a CIF file.
#**

# Note: this script assumes that atoms have unique labels in the input
# CIF file; most often these are labels given by the _atom_site_label
# tag. If the assumption of uniqness does not hold, the script
# attempts by default to create uniq labels itself, appanding numeric
# prefixes to the duplicate labels.
#
# The uniqueness of the labels is assumed in checks for atoms at
# special positions, and most importantly in the code removing
# duplicate molecules.
#
# Although there is an option to switch off this diversifivation of
# labels, the algorithms employed in this script will most probably
# break and give incorrect results (e.g. some atoms, namely ones with
# duplicate labels, will be missing from the output). Thus, use option
# '--dont-uniquify-atoms' with caution.

# ~/workspace/praktika/cod/cif/1/1000028.cif interesting
# kokia prasme, kai disorder_assembly reiksme nelygi ".", o disorder_group lygi.

use strict;
use lib "./lib/perl5";
use lib "./CIFParser";
use lib "./CIFTags";
use lib "./Spacegroups";
use AtomBricks;
use CIFParser;
use CIFTagCanonicalNames;
use SymopParse;
use SymopLookup;
use CIFTagPrint;
use CIFTagManage;
use File::Basename;
use SOptions;
use SUsage;
use Fractional;
use AtomProperties;

no warnings 'recursion';

my $Id = '$Id$';

my $debug;

my $sort_molecules = 1; # A flag indicating whether molecules should
                        # be sorted in the output (descending by atom
                        # number)

my $format = "%8.6f";
my $continue_on_errors = 0;
my $covalent_sensitivity = 0.35;
my $audit = 1;
my $uniquify_atoms = 1;
my $uniquify_molecules = 1;       # Leave only unique molecules in the output
my $exclude_zero_occupancies = 1; # Do not use atoms with zero
                                  # occupancies

# A fraction of covalent bond radii Used to determine when atoms are
# too close and are considered a bump:

my $bump_distance_factor = 0.75;

my $ignore_bumps = 0; # detect and warn about close atom "bumps"
                      # but do not stop processing.

# A distance between two atoms when they are considered to be
# instances of the same atom on a special position:

my $special_position_cutoff = 0.01;

my $cif_header_file; # Comments from the beginning of this file wil be
                     # prepended to the output.

@ARGV = getOptions
(
#** -c,--covalent-sensitivity
#**                    Set a new covalent sensitivity value (default 0.35)
#**
#** -h, --cif-header-file input_header.txt
#**                    Comments from the beginning of this file wil be
#**                    prepended to the output.
#**
#** -i,--ignore-bumps
#**                     Detect and warn about close atom "bumps" but do not
#**                     stop processing.
#**
#** -i-,--dont-ignore-bumps,--no-ignore-bumps
#**                     Stop processing immediately if bumps are
#**                     detected (default).
#**
#** -s,--sort-molecules
#**                     Sort molecules descending bey their atom types before
#**                     outputing them into the output CIF.
#**
#** -i-,--dont-sort-molecules,--no-sort-molecules
#**                     Do not sort molecules, print them out in the order they
#**                     are detected.
#**
#** --uniquify-atoms
#**                    Makes unique the labels of atoms (default).
#**
#** --no-uniquify-atoms,--dont-uniquify-atoms
#**                    Do not makes unique labels for atoms,
#**                    exclude duplicates.
#**
#*  --bump-distance-factor 0.75
#*         A fraction of covalent bond radii sum used to determine when
#*         atoms are too close and are considered a bump:
#*
#** --continue-on-errors
#**                    Do not stop if errors such as unrecognised atoms are
#**                    encountered; the output may be incorrect and missing
#**                    some atoms if this otion is used!
#**
#** --dont-continue-on-errors, --no-continue-on-errors
#**                    Stop immediately when an error is encountered.
#**                    
#** --exclude-zero-occupancies
#**                    Do not use atoms woth 0 occupancies in calculations
#**                    (defalut).
#**                    
#** --dont-exclude-zero-occupancies, --no-exclude-zero-occupancies
#**                    Use all atoms regardless of their occupancy.
#**
#** --debug            Print some human-readable debug output
#** --no-debug         Suppress any debug output (default)
#**
#** --format "%8.6f"   Use the specified format for output coordinate printout.
#**
#** --audit            print audit information to the generated CIF file
#**                    (default)
#** --no-audit         do not print audit information to the generated CIF file
#** --help,--usage     print short usage message (this message) and exit

    "--uniquify-atoms"      => sub { $uniquify_atoms = 1; },
    "--no-uniquify-atoms"   => sub { $uniquify_atoms = 0; },
    "--dont-uniquify-atoms" => sub { $uniquify_atoms = 0; },

    "-c,--covalent-sensitivity" => \$covalent_sensitivity,

    "-h,--add-cif-header" => \$cif_header_file,

    "-i,--ignore-bumps"   => sub{ $ignore_bumps = 1 },
    "--no-ignore-bumps"   => sub{ $ignore_bumps = 0 },
    "--dont-ignore-bumps" => sub{ $ignore_bumps = 0 },

    "-s,--sort-molecules"   => sub{ $sort_molecules = 1 },
    "--no-sort-molecules"   => sub{ $sort_molecules = 0 },
    "--dont-sort-molecules" => sub{ $sort_molecules = 0 },

    "--continue-on-errors" => sub{ $continue_on_errors = 1 },
    "--no-continue-on-errors" => sub{ $continue_on_errors = 0 },
    "--dont-continue-on-errors" => sub{ $continue_on_errors = 0 },

    "--exclude-zero-occupancies"    => sub { $exclude_zero_occupancies = 1; },
    "--no-exclude-zero-occupancies" => sub { $exclude_zero_occupancies = 0; },
    "--dont-exclude-zero-occupancies" => sub { $exclude_zero_occupancies = 0; },

    "--remove-duplicate-molecules"      => sub { $uniquify_molecules = 1; },
    "--no-remove-duplicate-molecules"   => sub { $uniquify_molecules = 0; },
    "--dont-remove-duplicate-molecules" => sub { $uniquify_molecules = 0; },

    "--bump-distance-factor" => \$bump_distance_factor,

    "--debug"    => sub { $debug = 1 },
    "--no-debug" => sub { $debug = 0 },

    "--format" => \$format,

    "--audit"                   => sub { $audit = 1; },
    "--no-audit"                => sub { $audit = 0; },
    "--help,--usage"            => sub { SUsage::usage; exit },
);

# Error reporting (message formatting) subroutines:

sub print_error($$$$$)
{
    my ( $program, $filename, $datablock, $errlevel, $message ) = @_;

    print STDERR $program, ": ", $filename,
    defined $datablock ? " data_" . $datablock : "",
    defined $errlevel ? ": " . $errlevel : "",
    ": ", $message, ".\n";
}

sub error($$$$)
{
    my ( $program, $filename, $datablock, $message ) = @_;
    print_error( $program, $filename, $datablock, "ERROR", $message );
}

sub warning($$$$)
{
    my ( $program, $filename, $datablock, $message ) = @_;
    print_error( $program, $filename, $datablock, "WARNING", $message );
}

my $cif_header;

if( $cif_header_file ) {
    open( HDR, "$cif_header_file" ) or
        die( "$0: could not open header file '$cif_header_file' " .
             "for input - $!" );

    $cif_header = "";
    while( <HDR> ) {
        last unless /^#/;
        $cif_header .= $_;
    }

    close( HDR ) or
        die( "$0: error reading header file '$cif_header_file' - $!" );
}

my $filename = shift(@ARGV);
my $parser = new CIFParser;
my $data = $parser->Run($filename);

canonicalize_all_names( $data );

if( $cif_header ) {
    print $cif_header;
}

for my $dataset (@$data)
{
#   extracts atom site label or atom site type symbol
    my $values = $dataset->{"values"};
    my $atom_site_tag;

    if (exists $values->{"_atom_site_label"})
    {
        $atom_site_tag = "_atom_site_label";
    }
    elsif (exists $values->{"_atom_site_type_symbol"})
    {
        $atom_site_tag = "_atom_site_type_symbol";

        error( $0, $filename, $dataset->{name},
               "_atom_site_label tag was not found. " .
               "A serial number will be appended " .
               "to _atom_site_type_symbol to make atom labels" );
    }
    else
    {
        error( $0, $filename, $dataset->{name},
               "neither _atom_site_type_symbol nor _atom_site_label " .
               "were found" );
    }

#   extracts symmetry operators
    my $sym_data;

    if( exists $values->{"_space_group_symop_operation_xyz"} ) {
        $sym_data = $values->{"_space_group_symop_operation_xyz"};
    } elsif( exists $values->{"_symmetry_equiv_pos_as_xyz"} ) {
        $sym_data = $values->{"_symmetry_equiv_pos_as_xyz"};
    }

    if( exists $values->{"_space_group_name_Hall"} &&
        not defined $sym_data ) {
        my $hall = $values->{"_space_group_name_Hall"}[0];
        $sym_data = get_symops("hall", $hall);

        if( !defined $sym_data )
        {
            error( $0, $filename, $dataset->{name},
                   "incorrect _space_group_name_Hall '$hall'" );
        }
    } elsif( exists $values->{"_symmetry_space_group_name_Hall"} &&
        not defined $sym_data ) {
        my $hall = $values->{"_symmetry_space_group_name_Hall"}[0];
        $sym_data = get_symops("hall", $hall);

        if( !defined $sym_data )
        {
            error( $0, $filename, $dataset->{name},
                   "incorrect _symmetry_space_group_name_Hall '$hall'" );
        }
    }

    if(exists $values->{"_space_group_name_H-M_alt"} &&
       not defined $sym_data)
    {
        my $h_m = $values->{"_space_group_name_H-M_alt"}[0];
        $sym_data = get_symops("hermann_mauguin", $h_m);

        if( !defined $sym_data )
        {
            error( $0, $filename, $dataset->{name},
                   "incorrect _space_group_name_H-M_alt '$h_m'" );
        }
    } elsif(exists $values->{"_symmetry_space_group_name_H-M"} &&
            not defined $sym_data)
    {
        my $h_m = $values->{"_symmetry_space_group_name_H-M"}[0];
        $sym_data = get_symops("hermann_mauguin", $h_m);

        if( !defined $sym_data )
        {
            error( $0, $filename, $dataset->{name},
                   "incorrect _symmetry_space_group_name_H-M '$h_m'" );
        }
    }

    if(not defined $sym_data)
    {
        error( $0, $filename, $dataset->{name},
               "no symmetry information found" );
    }

    if((defined $atom_site_tag) && (defined $sym_data) && ($sym_data != 0))
    {
        my $molecule_id = 0;
        my $dataset_name = $dataset->{name};

        my $unique_molecules = get_molecules (  $covalent_sensitivity,
                                                $filename, $dataset_name,
                                                $sym_data,
                                                $atom_site_tag,
                                                $values,
                                                \%AtomProperties::atoms,
                                                $uniquify_atoms );

#       prints molecule to the CIF file

        if( $sort_molecules ) {
            @$unique_molecules = sort {@{$b->{atoms_c}} <=> @{$a->{atoms_c}}}
                                 @$unique_molecules;
        }

        foreach my $molecule (@$unique_molecules)
        {
            print_molecule( $molecule_id, $audit, $molecule, $Id,
                            $dataset, $dataset_name, $filename );
            $molecule_id++;
        }
    }
}

#==============================================================================#
# Forward subroutine definitions:

sub symgen_atom($$$);
sub symgen_all_atoms($$$);
sub apply_shifts($);
sub find_molecules($$$$$$$$$);
sub find_molecule($$$$$$$$$$$);
sub check_init_atoms($$$$);
sub initial_atoms($$$$$);

sub test_bump($$$$$$$);

#==============================================================================#
# Find a maximal covalent radius in the atom property list

sub get_max_covalent_radius($)
{
    my ($atom_properties) = @_;

    my $max_radius = 0;

    for my $atom (keys %$atom_properties) {
        if( $max_radius < $atom_properties->{$atom}{covalent_radius} ) {
            $max_radius = $atom_properties->{$atom}{covalent_radius};
        }
    }

    return $max_radius;
}

#==============================================================================#
# This is the main function where other functions such as find_molecules are
# called.
# Accepts
#     covalent_sensitivity - a threshold for covalent sensitivity
#     filename             - CIF file name
#     sym_data             - symmetric data from the CIF file
#     atom_site_tag        - atom site label or atom site type symbol from the
#                            CIF file
#     values               - a hash where a data from the CIF file is stored
#
# Returns
#     unique_molecules     - an array of hashes
#                     %molecule = (
#                         atoms_c=>[\%atom_info1, \%atom_info2], #covalent bond
#                         chemical_formula_sum=>"C6 H6",
#                                 );

sub get_molecules
{
    my $covalent_sensitivity = shift;
    my $filename             = shift;
    my $dataname             = shift;
    my $sym_data             = shift;
    my $atom_site_tag        = shift;
    my $values               = shift;
    my $atom_properties      = shift;
    my $uniquify_atoms       = shift;

    #   extracts symmetry operators
    my @sym_operators = map { symop_from_string($_) } @{$sym_data};

    #   extracts cell angles(alpha, beta, gama) and lengths(a, b, c)
    my @cell_lengths_and_angles = extract_angles_and_lengths( $values,
                                                              $filename,
                                                              $dataname );

    #   extracts atoms fract coordinates
    my $all_initial_atoms = initial_atoms( $atom_properties,
                                           $values, $atom_site_tag,
                                           $filename, $dataname );

    if( @$all_initial_atoms == 0 ) {
        warning( $0, $filename, $dataname,
                 "no atoms suitable for processing were found" );
        print STDERR $0, ": ",
            "Maybe all occupancies were unkown, zero, or " .
            "all atom types were unrecognised?\n";
        return [];
    }

    #   checks if all atom labels are unique, otherwise appends ID to the label.
    my $checked_initial_atoms = check_init_atoms($all_initial_atoms,
                                                 $uniquify_atoms,
                                                 $filename, $dataname );

    #   splits init atoms into assemblies and groups
    my $atom_groups = atom_groups($checked_initial_atoms);

    my $max_covalent_radius = get_max_covalent_radius( $atom_properties );

    my @unique_molecules;
    my %seen_molecules;

    foreach my $atom_group ( @{$atom_groups} )
    {
        my $translated_atoms = init_atoms_modulo_1($atom_group);

        my $ortho_matrix = symop_ortho_from_fract(@cell_lengths_and_angles);

        my @symmetric_atoms = apply_shifts( 
            symgen_all_atoms( $translated_atoms, \@sym_operators, 
                              $ortho_matrix ));

        foreach my $atom (@symmetric_atoms)
        {
            $atom->{coordinates_ortho} =
                multiply_matrix_with_vector( $ortho_matrix,
                                             $atom->{coordinates_fract} );
        }

        do {
            use Serialise;
            serialiseRef( \@symmetric_atoms );
        } if $debug;

        my $bricks = AtomBricks::build_bricks( \@symmetric_atoms,
                                               $max_covalent_radius * 2 +
                                               $covalent_sensitivity );

        #   Finds molecules
        push( @unique_molecules, find_molecules( $covalent_sensitivity,
                                                 $atom_properties,
                                                 \@symmetric_atoms,
                                                 $translated_atoms,
                                                 $bricks,
                                                 $filename, $dataname,
                                                 \%seen_molecules,
                                                 $ortho_matrix ));
    }

    # Calculates chemical formula sum
    foreach my $molecule (@unique_molecules)
    {
        $molecule->{chemical_formula_sum} =
                                chemical_formula_sum( $molecule->{atoms_c});
    }

    return \@unique_molecules;
}

# ============================================================================ #
# Accepts an array of
#
#   $atom_info = {
#                   label=>"C1_2",
#                   chemical_type=>"C",
#                   coordinates_fract=>[1.0, 1.0, 1.0],
#                   unity_matrix_applied=>1,
#                   assembly=>"A", # "."
#                   group=>"1", # "."
#              }
#
sub check_init_atoms($$$$)
{
    my ($init_atoms, $uniquify_atoms, $filename, $dataset_name) = @_;

    my $max_label_suffix = 30000; # Maximum number to be appened to labels 
                                  # when trying to produce unique names.

    my @checked_initial_atoms;

    my %used_labels;
    my @labels_to_be_renamed;

    foreach my $atom (@{$init_atoms})
    {
        my $atom_copy = copy_atom( $atom );
        my $label = $atom->{label};

        push( @checked_initial_atoms, $atom_copy );

        if( ! exists $used_labels{$label} ) {
            $used_labels{$label}{atoms} = [ $atom_copy ];
        } else {
            push( @{$used_labels{$label}{atoms}}, $atom_copy );
            warning( $0, $filename, $dataset_name,
                     "atom label '$label' is not unique" );
            push( @labels_to_be_renamed, $label );
        }
        $used_labels{$label}{count} ++;
    }

    if( $uniquify_atoms )
    {
        foreach my $label (@labels_to_be_renamed)
        {
            foreach my $renamed_atom (@{$used_labels{$label}{atoms}}) {
                my $id = 0;
                while( exists $used_labels{$label . "_" .$id} &&
                       $id <= $max_label_suffix ) {
                    $id ++;
                }
                if( $id > $max_label_suffix ) {
                    error( $0, $filename, $dataset_name,
                           "could not generate unique atom name for ".
                           "atom '$label', even after $id iterations" );
                }
                my $new_label = $label . "_" . $id;
                warning( $0, $filename, $dataset_name,
                         "renaming atom '$label' " .
                         "to '" . $new_label . "'" );
                $renamed_atom->{label}          = $new_label;
                $renamed_atom->{label_basename} = $new_label;
                $used_labels{$new_label}{count} ++;
            }
        }
    }

    return \@checked_initial_atoms;
}

# ============================================================================ #
# Returns an array of
#
#   $atom_info = {
#                   label=>"C1_2",
#                   chemical_type=>"C",
#                   atom_site_type_symbol = "C",
#                   coordinates_fract=>[1.0, 1.0, 1.0],
#                   unity_matrix_applied=>1,
#                   assembly=>"A", # "."
#                   group=>"1", # "."
#              }
#
sub initial_atoms($$$$$)
{
    my ($atom_properties, $values, $atom_site_tag, $filename, $dataname) = @_;

    my $atom_labels = $values->{$atom_site_tag};

    my @all_initial_atoms;

    for (my $i = 0; $i < @{$atom_labels}; $i++)
    {
        if( $exclude_zero_occupancies &&
            defined $values->{_atom_site_occupancy} ) {
            my $occupancy = $values->{_atom_site_occupancy}[$i];
            $occupancy =~ s/\(\d+\)$//; # remove precission
            if( $occupancy eq "?" || $occupancy eq "." ||
                $occupancy == 0.0 ) {
                next;
            }
        }

        my $atom_info = extract_atom($$atom_labels[$i], $values, $i);

        if ( $atom_site_tag eq "_atom_site_type_symbol" )
        {
            $atom_info->{"label"} .= $i;
            $atom_info->{"label_basename"} = $atom_info->{"label"};
        }

        if (exists $atom_properties->{ $atom_info->{"chemical_type"}})
        {
            push( @all_initial_atoms, $atom_info );
        }
        else
        {
            my $message = "unknown chemical type '" .
                $atom_info->{chemical_type} . "'";

            if( $continue_on_errors ) {
                warning( $0, $filename, $dataname, $message );
            } else {
                error( $0, $filename, $dataname, $message );
                exit(1);
            }
        }
    }
    return \@all_initial_atoms;
}


# ============================================================================ #
# Accepts init atoms.
# Returns the same atoms but with applied modulo_1 to all coords.

sub init_atoms_modulo_1
{
    my ($init_atoms) = @_;

    my @initial_atoms_modulo_1;

    foreach my $atom (@$init_atoms)
    {
        my $init_atom_modulo_1 = copy_atom($atom);
        my @modulo_1_coords = map {modulo_1($_)} @{$atom->{coordinates_fract}};
        $init_atom_modulo_1->{coordinates_fract} = \@modulo_1_coords;
        push(@initial_atoms_modulo_1, $init_atom_modulo_1);
    }

    return \@initial_atoms_modulo_1;
}

#============================================================================= #
# It's a function where atom groups are made according disorder information. If
# there is only one disorder assembly then all possible atom groups will be
# generated. If there is more then one disorder assembly desicion will be made
# according the following example:
# an assembly disorder A has three disorder groups and B - two. Then the
# following groups will be made: (1,1) (2,2) (3,2).

# Accepts
#   initial_atoms - an array of references to
#   $atom_info = {
#               label=>"C1_2",
#               chemical_type=>"C",
#               coordinates_fract=>[1.0, 1.0, 1.0],
#               unity_matrix_applied=>1,
#               assembly=>"A", # "."
#               group=>"1", # "."
#              }
# Returns
#   groups - an array of references to arrays of references to
#   $atom_info = {
#               label=>"C1_2",
#               chemical_type=>"C",
#               coordinates_fract=>[1.0, 1.0, 1.0],
#               unity_matrix_applied=>1,
#               assembly=>"A", # "."
#               group=>"1", # "."
#              }
# These arrays of references are generated atom groups.

sub atom_groups
{
    my ($initial_atoms) = @_;

    my $assemblies = assemblies($initial_atoms);

#     for my $assembly (keys %$assemblies)
#     {
#         print STDERR "Assembly $assembly\n";
#
#         foreach my $group (@{$assemblies->{$assembly}})
#         {
#             print STDERR "$group ";
#         }
#         print STDERR "\n";
#     }

    if((keys %$assemblies) == 0)
    {
        my @one_assembly;
        push(@one_assembly, $initial_atoms);
        return \@one_assembly;
    }

    my @keys = keys %$assemblies;
    my @atom_groups;

    if(@keys == 1)
    {
        my $assembly = $keys[0];
        my $groups = $assemblies->{$assembly};

        foreach my $group (@$groups)
        {
            my @tmp_group;
            foreach my $atom (@$initial_atoms)
            {
                if($atom->{group} eq $group && $atom->{assembly} eq $assembly)
                {
                    push(@tmp_group, $atom);
                }
            }
            push(@atom_groups, \@tmp_group);
        }
    }
    else
    {
        my $iteration_number = 0;

        foreach my $assembly (@keys)
        {
            my $groups = $assemblies->{$assembly};
            if($iteration_number < @$groups)
            {
                $iteration_number = @$groups;
            }
        }

        for(my $i = 0; $i < $iteration_number; $i++)
        {
            my @group;
            foreach my $assembly (@keys)
            {
                my $groups = $assemblies->{$assembly};
                my $atom_group;
                if($i < @$groups)
                {
                    $atom_group = $$groups[$i];
                }
                else
                {
                    $atom_group = $$groups[-1];
                }

                foreach my $atom (@$initial_atoms)
                {
                    if($atom->{group} eq $atom_group &&
                                    $atom->{assembly} eq $assembly)
                    {
                        push(@group, $atom);
                    }
                }
            }
            push(@atom_groups, \@group);
        }
    }

    # Appends those atoms which do not belong to any group or assembly

    my @independent_atoms;
    foreach my $atom (@$initial_atoms)
    {
        if($atom->{group} eq ".")
        {
            push(@independent_atoms, $atom);
        }
    }

    foreach my $group (@atom_groups)
    {
        push(@$group, @independent_atoms);
    }

    return \@atom_groups;
}

# ============================================================================ #
# Returns a hash of all possible assemblies and groups:
# %assemblies = ( A => [1,2,3],
#                 . => [1,2]);

sub assemblies
{
    my ($init_atoms) = @_;

    my %assemblies;

    foreach my $atom (@$init_atoms)
    {
        if(not exists $assemblies{$atom->{assembly}})
        {
            my @groups;
            $assemblies{$atom->{assembly}} = \@groups;
        }
    }

    for my $assembly (keys %assemblies)
    {
        my %unique_groups;

        foreach my $atom (@$init_atoms)
        {
            if((not exists $unique_groups{$atom->{group}}) &&
               ($assembly eq $atom->{assembly}) &&
               ($atom->{group} ne "."))
            {
                $unique_groups{$atom->{group}} = $atom->{group};
                push(@{$assemblies{$assembly}}, $atom->{group});
            }
        }
    }

    for my $assembly (keys %assemblies)
    {
        if(@{$assemblies{$assembly}} == 0)
        {
            delete $assemblies{$assembly};
        }
    }

   return \%assemblies;
}

#===============================================================#
# Extract unit cell angles and lengths.

# Accepts
#     values - a hash where a data from the CIF file is stored
# Returns
#     cell_lengths_and_angles - an array  with stored information.

sub extract_angles_and_lengths($$$)
{
    my( $values, $filename, $dataname ) = @_;

    my @cell_lengths_and_angles;

    for my $cif_tag (qw(_cell_length_a
                        _cell_length_b
                        _cell_length_c
                        ))
    {
        push(@cell_lengths_and_angles, $values->{$cif_tag}[0]);
        $cell_lengths_and_angles[-1] =~ s/\(\d+\)$//;
    }

    for my $cif_tag (qw(_cell_angle_alpha
                        _cell_angle_beta
                        _cell_angle_gamma
                        ))
    {
        if( exists $values->{$cif_tag} ) {
            push( @cell_lengths_and_angles, $values->{$cif_tag}[0] );
            $cell_lengths_and_angles[-1] =~ s/\(\d+\)$//;
        } else {
            warning( $0, $filename, $dataname,
                     "cell angle '$cif_tag' not present -- " .
                     "taking default value 90 degress." );
            push( @cell_lengths_and_angles, 90 );
        }
    }

    return @cell_lengths_and_angles;
}

#===============================================================#
# Extracts atom information from the CIF file.

# Accepts
#     values     - a hash where a data from the CIF file is stored
#     atom_label - atom site label or atom site type symbol from the CIF file
#     number     - a number of the current atom

# Returns a hash $atom_info = {
#                       label=>"C1_2",
#                       chemical_type=>"C",
#                       atom_site_type_symbol = "C",
#                       coordinates_fract=>[1.0, 1.0, 1.0],
#                       unity_matrix_applied=>1,
#                       assembly=>"A", # "."
#                       group=>"1", # "."
#                       }

sub extract_atom
{
    my($atom_label, $values, $number) = @_;

    my %atom_info;
    my @atom_xyz;

    for my $cif_fract ( "_atom_site_fract_x",
                        "_atom_site_fract_y",
                        "_atom_site_fract_z",)
    {
        push(@atom_xyz, $values->{$cif_fract}[$number]);
        $atom_xyz[-1] =~ s/\(\d+\)$//;
    }

    $atom_info{"coordinates_fract"}     = \@atom_xyz;
    $atom_info{"label"}                 = $atom_label;
    $atom_info{"label_basename"}        = $atom_label;
    $atom_info{"symop_id"}              = (-1) * $number - 1;
    $atom_info{"unity_matrix_applied"}  = 1;

    my $atom_type;

    if( exists $values->{_atom_site_type_symbol}  &&
        defined $values->{_atom_site_type_symbol}[$number] &&
        $values->{_atom_site_type_symbol}[$number] ne '?' ) {
        $atom_type = $values->{_atom_site_type_symbol}[$number];
        $atom_info{atom_site_type_symbol} = $atom_type;
        if( $atom_type =~ m/^([A-Za-z]{1,2})/ ) {
            $atom_type = ucfirst( lc( $1 ));
        }
    } else {
        if( $values->{_atom_site_label}[$number] =~
            m/^([A-Za-z]{1,2})/ ) {
            $atom_type = ucfirst( lc( $1 ));
        } else {
            die( "could not determine atom type for atom " .
                 "'$values->{_atom_site_label}[$number]'" );
        }
    }

    $atom_info{"chemical_type"} = $atom_type;

    if(exists $values->{"_atom_site_disorder_assembly"}[$number])
    {
        $atom_info{"assembly"} =
                        $values->{"_atom_site_disorder_assembly"}[$number];
    }
    else
    {
        $atom_info{"assembly"} = ".";
    }

    if(exists $values->{"_atom_site_disorder_group"}[$number])
    {
        $atom_info{"group"} = $values->{"_atom_site_disorder_group"}[$number];
    }
    else
    {
        $atom_info{"group"} = ".";
    }

    return \%atom_info;
}

#===============================================================#
# Copies atom and returns the same instance of it (different object, same props)

# Accepts a hash $atom_info = {
#                       label=>"C1_2",
#                       label_basename=>"C1",
#                       chemical_type=>"C",
#                       coordinates_fract=>[1.0, 1.0, 1.0],
#                       coordinates_ortho=>[5.0, -1.3, 1.7],
#                       unity_matrix_applied=>1,
#                       symop_id=>1
#                       assembly=>"A", # "."
#                       group=>"1", # "."
#                       }

# Returns a hash $new_atom_info = {
#                       label=>"C1_2",
#                       label_basename=>"C1",
#                       chemical_type=>"C",
#                       coordinates_fract=>[1.0, 1.0, 1.0],
#                       coordinates_ortho=>[5.0, -1.3, 1.7],
#                       unity_matrix_applied=>1,
#                       symop_id=>1,
#                       assembly=>"A", # "."
#                       group=>"1", # "."
#                       }

sub copy_atom
{
    my($old_atom) = @_;

    my %new_atom;

    if( ref $old_atom ne "HASH" ) {
        use Carp;
        croak;
    }

    for my $key (keys %$old_atom ) {
        if( !ref $old_atom->{$key} ) {
            $new_atom{$key} = $old_atom->{$key};
        } elsif( ref $old_atom->{$key} eq "ARRAY" ) {
            $new_atom{$key} = copy_array($old_atom->{$key});
        } else {
            die( "assertion failed: 'copy_atom' does not know how to " .
                 "copy the supplied object" );
        }
    }

    return \%new_atom;
}

sub copy_array
{
    my ($arr) = @_;

    my @copy_arr;
    foreach my $value (@$arr)
    {
        push(@copy_arr, $value);
    }

    return \@copy_arr;
}

#===============================================================#
# Gets symmetry operators if they are not directly represented in the CIF file.

# Accepts
#     option - an option, for example, "hall"
#     param  - according to an option a value given in the CIF file

# Uses @SymopLookup::table =
# (
# {
#     number          => 1,
#     hall            => ' P 1',
#     schoenflies     => 'C1^1',
#     hermann_mauguin => 'P 1',
#     universal_h_m   => 'P 1',
#     symops => [
#         'x,y,z',
#     ],
#     ncsym => [
#         'x,y,z',
#     ]
# },
# );

# Return a reference to symmetry operators.


sub get_symops
{
    my ($option, $param) = @_;

    $param =~ s/ //g;
    $param =~ s/_//g;

    foreach my $hash (@SymopLookup::table)
    {
        my $value = $hash->{$option};
        $value =~ s/ //g;
        $value =~ s/_//g;

        if($value eq $param)
        {
            return $hash->{symops};
        }
    }
    return undef;
}
#===============================================================#
# Made a decision if a chemical bond exists.

# Accepts distance matrix, atom id and a reference to a hash of atom properties
# %atoms = (
#           H => { #(chemical_type)
#                     name => Hydrogen,
#                     period => 1,
#                     group => 1,
#                     block => s,
#                     atomic_number => "1",
#                     atomic_weight => 1.008,
#                     covalent_radius => 0.23,
#                     vdw_radius => 1.09,
#                     valency => [1],
#                     },
#          );

sub test_bond
{
    my ($atom_properties, $chemical_type1, $chemical_type2, $distance,
        $covalent_sensitivity) = @_;

    my $cov_radius1 = $atom_properties->{$chemical_type1}->{covalent_radius};
    my $cov_radius2 = $atom_properties->{$chemical_type2}->{covalent_radius};

    if($distance < $cov_radius1 + $cov_radius2 + $covalent_sensitivity)
    {
        return 1;
    }

    return 0;
}

#===============================================================#
# Makes a decision if atoms are too close to each other, i.e if they
# make a "bump:

# Accepts distance, atom id and a reference to a hash of atom properties
# %atoms = (
#           H => { #(chemical_type)
#                     name => Hydrogen,
#                     period => 1,
#                     group => 1,
#                     block => s,
#                     atomic_number => "1",
#                     atomic_weight => 1.008,
#                     covalent_radius => 0.23,
#                     vdw_radius => 1.09,
#                     valency => [1],
#                     },
#          );

sub test_bump($$$$$$$)
{
    my ( $atom_properties, $chemical_type1, $chemical_type2,
         $atom1_label, $atom2_label,
         $dist, $bump_factor ) = @_;

    my $cov_radius1 = $atom_properties->{$chemical_type1}->{covalent_radius};
    my $cov_radius2 = $atom_properties->{$chemical_type2}->{covalent_radius};

    if( $dist < $bump_factor * ($cov_radius1 + $cov_radius2) &&
        ($dist > $special_position_cutoff ||
         $atom1_label ne $atom2_label)) {
        return 1;
    }

    return 0;
}

#==============================================================#
# Finds translation center of mass and center of mass modulo 1 information.

# Accepts two arrays of coordinates_fract.

# Returns an array of differences between coordinates_fract.

sub translation
{
    my ($coords, $coords_modulo_1) = @_;

    my @translation;
    for(my $i = 0; $i < @{$coords}; $i++)
    {
        push(@translation, ${$coords}[$i] - ${$coords_modulo_1}[$i]);
    }

    return \@translation;
}

#==============================================================#
# Translates all initial atoms according calculated translation.

# Accepts an arrays of initial atoms and translation.

# Returns an array of translated initial atoms.

sub translate_atoms
{
    my($initial_atoms, $translation) = @_;
    my @translated_initial_atoms;

    foreach my $atom_info (@{$initial_atoms})
    {
        my $new_atom_info = copy_atom($atom_info);
        my @new_atom_xyz;

        push(@new_atom_xyz,$atom_info->{"coordinates_fract"}[0] +
                                                            ${$translation}[0]);
        push(@new_atom_xyz,$atom_info->{"coordinates_fract"}[1] +
                                                            ${$translation}[1]);
        push(@new_atom_xyz,$atom_info->{"coordinates_fract"}[2] +
                                                            ${$translation}[2]);

        $new_atom_info->{"coordinates_fract"} = \@new_atom_xyz;

        push(@translated_initial_atoms, $new_atom_info);
    }

    return \@translated_initial_atoms;
}

#==============================================================#
# Translates an atom according a given translation.
#
# Accepts an atom description and a translation.
#
# Returns a translated atom.

sub translate_atom
{
    my($atom, $translation) = @_;

    my $new_atom = copy_atom( $atom );
    my @new_atom_xyz;

    push( @new_atom_xyz, $atom->{"coordinates_fract"}[0] +
          ${$translation}[0] );
    push( @new_atom_xyz, $atom->{"coordinates_fract"}[1] +
          ${$translation}[1] );
    push( @new_atom_xyz, $atom->{"coordinates_fract"}[2] +
          ${$translation}[2] );

    $new_atom->{"coordinates_fract"} = \@new_atom_xyz;
    delete( $new_atom->{coordinates_ortho} );

    return $new_atom;
}

#===============================================================#
# Finds a molecule chemical formula sum.

# Accepts an array of atoms:
# $atom =      {label=>"C1_2",
#               chemical_type=>"C",
#               coordinates_fract=>[1.0, 1.0, 1.0],
#               unity_matrix_applied=>1}

# Returns a string with chemical formula sum.

sub chemical_formula_sum
{
    my ($atoms) = @_;

    my %chemical_types;

    foreach my $atom (@{$atoms})
    {
        my $chemical_type = $atom->{chemical_type};
        if(defined $chemical_types{$chemical_type})
        {
            $chemical_types{$chemical_type} = ++$chemical_types{$chemical_type};
        }
        else
        {
            $chemical_types{$chemical_type} = 1;
        }
    }

    my $formula_sum = "";

    # Chemical formulas, according the IUCr recommendations, should
    # use the 'Hill' system used by Chemical Abstracts:
    #
    # the order of the elements within any group or moiety depends on
    # whether carbon is present or not. If carbon is present, the
    # order should be: C, then H, then the other elements in
    # alphabetical order of their symbol. If carbon is not present,
    # the elements are listed purely in alphabetical order of their
    # symbol.

    # References:
    # 1. http://www.iucr.org/__data/iucr/cifdic_html/1/cif_core.dic/Cchemical_formula.html
    # (2009-06-25)
    #
    # 2. Dictionary of organic compounds By J. I. G. (John Ivan
    # George) Cadogan, American Chemical Society. Chemical Abstracts
    # Service, P. H. Rhodes, Steven V Ley; page 106 (found by Google
    # book search on 2009-06-25)

    if( exists $chemical_types{C} ) {
        if( $chemical_types{C} == 1 ) {
            $formula_sum = "C ";
        } else {
            $formula_sum = "C" . $chemical_types{C} . " ";
        }
        delete $chemical_types{C};
        if( exists $chemical_types{H} ) {
            if( $chemical_types{H} == 1 ) {
                $formula_sum .= "H ";
            } else {
                $formula_sum .= "H" . $chemical_types{H} . " ";
            }
            delete $chemical_types{H};
        }
    }

    foreach my $key (sort keys %chemical_types)
    {
        if($chemical_types{$key} == 1)
        {
            $formula_sum .= $key . " ";
        }
        else
        {
            $formula_sum .= $key . $chemical_types{$key} . " ";
        }
    }
    $formula_sum =~ s/\s$//;

    return $formula_sum;
}
#===============================================================#
# Multiplies an ortho matrix with a vector.

# my $ortho = [
#     [ o11 o12 o13 ]
#     [ 0   o22 o23 ]
#     [ 0   0   o33 ]
# ]
#

sub multiply_matrix_with_vector
{
    my($matrix, $vector) = @_;

    my @new_coordinates;

    for(my $i = 0; $i < @{$vector}; $i++)
    {
        $new_coordinates[$i] = 0;
        for(my $j = 0; $j < @{$vector}; $j++)
        {
            $new_coordinates[$i] += ${$matrix}[$i][$j] * ${$vector}[$j];
        }
    }

    return \@new_coordinates;
}


#===============================================================#
# Applies symmetry operator to a given atom.

# The symop_apply subroutine accepts a reference to a hash
# $atom_info = {label=>"C1_2",
#               chemical_type=>"C",
#               coordinates_fract=>[1.0, 1.0, 1.0],
#               unity_matrix_applied=>1} and
# a refernce to an array - symmetry operator
# my $symop = [
#     [ r11 r12 r13 t1 ]
#     [ r21 r22 r23 t1 ]
#     [ r31 r32 r33 t1 ]
#     [   0   0   0  1 ]
# ],
# Returns an above-mentioned hash.
#
sub symop_apply
{
    my($atom_info, $symop, $symop_id) = @_;

    my $new_atom_info = copy_atom($atom_info);

    my @new_atom_xyz;
    my $atom_xyz = $atom_info->{"coordinates_fract"};

    for(my $i = 0; $i < @{$atom_xyz}; $i++)
    {
        $new_atom_xyz[$i] = 0;
        for(my $j = 0; $j < @{$atom_xyz}; $j++)
        {
            $new_atom_xyz[$i] += ${$atom_xyz}[$j] * ${$symop}[$i][$j];
        }
        $new_atom_xyz[$i] += ${$symop}[$i][-1];
        $new_atom_xyz[$i] = modulo_1($new_atom_xyz[$i]);
    }

    $new_atom_info->{"symop_id"} = $symop_id;
    $new_atom_info->{"coordinates_fract"} = \@new_atom_xyz;
    $new_atom_info->{"unity_matrix_applied"} = symop_is_unity($symop);
    if( !$new_atom_info->{"unity_matrix_applied"} ) {
        $new_atom_info->{"label"} = $atom_info->{"label"} . "_" . $symop_id;
    }
    do {
        use Serialise;
        serialiseRef( { atom => $new_atom_info, symop => $symop } );
    } if 0;

    return $new_atom_info;
}

#===============================================================#
# Multiplies an ortho matrix with a vector.

# my $ortho = [
#     [ o11 o12 o13 ]
#     [ 0   o22 o23 ]
#     [ 0   0   o33 ]
# ]
#

sub mat_vect_mul($$)
{
    my($matrix, $vector) = @_;

    my @new_coordinates;

    for(my $i = 0; $i < @{$vector}; $i++)
    {
        $new_coordinates[$i] = 0;
        for(my $j = 0; $j < @{$vector}; $j++)
        {
            $new_coordinates[$i] += ${$matrix}[$i][$j] * ${$vector}[$j];
        }
    }

    if( @$vector == 3 && @$matrix == 4 ) {
        $new_coordinates[0] += $matrix->[0][3];
        $new_coordinates[1] += $matrix->[1][3];
        $new_coordinates[2] += $matrix->[2][3];
    }

    return \@new_coordinates;
}

#===============================================================#
# Checks whether atoms are in the same point in space:

sub atoms_coincide($$$)
{
    my ( $old_atom, $new_atom, $f2o ) = @_;

    my $old_coord = [ map { modulo_1($_) } @{$old_atom->{coordinates_fract}} ];
    my $old_xyz = mat_vect_mul( $f2o, $old_coord );

    my $new_coord = [ map { modulo_1($_) } @{$new_atom->{coordinates_fract}} ];

    for my $dx (-1, 0, 1) {
    for my $dy (-1, 0, 1) {
    for my $dz (-1, 0, 1) {
        my $shifted_coord = [
            $new_coord->[0] + $dx,
            $new_coord->[1] + $dy,
            $new_coord->[2] + $dz,
        ];
        my $new_xyz = mat_vect_mul( $f2o, $shifted_coord );
        if( distance( $new_xyz, $old_xyz ) < $special_position_cutoff ) {
            ## local $, = ", ";
            ## print ">>> mapped to self: @{$new_xyz} / @{$old_xyz}\n";
            return 1;
        }
    }}}

    return 0;
}

#===============================================================#
# Generate symmetry equivalents of an atom, exclude duplicates
# on special positions

sub symgen_atom($$$)
{
    my ( $atom, $sym_operators, $f2o ) = @_;

    my @sym_atoms;

    my $gp_multiplicity = int(@$sym_operators);
    my $multiplicity_ratio = 1;

    do {
        use Serialise;
        serialiseRef( $sym_operators );
    } if 0;

    my $n = 1;

    for my $symop ( @{$sym_operators} ) {
        my $new_atom = symop_apply( $atom, $symop, $n++ );
        if( !symop_is_unity( $symop ) &&
            atoms_coincide( $atom, $new_atom, $f2o )) {
            $multiplicity_ratio ++;
        } else {
            push( @sym_atoms, $new_atom );
        }
    }

    ## print ">>> $gp_multiplicity / $multiplicity_ratio\n";

    if( $gp_multiplicity % $multiplicity_ratio ) {
        die( "Multiplicity ratio $multiplicity_ratio does not divide " .
             "multiplicity of a general position $gp_multiplicity" .
             "-- this can not be." );
    }

    my $multiplicity = $gp_multiplicity / $multiplicity_ratio;

    for my $atom (@sym_atoms) {
        $atom->{multiplicity} = $multiplicity;
        $atom->{multiplicity_ratio} = $multiplicity_ratio;
    }

    if( $multiplicity_ratio == 1 ) {
        return @sym_atoms;
    } else {
        my @unique_atoms;
        my %to_be_deleted;
        for my $i (0..$#sym_atoms-1) {
            for my $j ($i+1..$#sym_atoms) {
                if( distance( $sym_atoms[$i]->{coordinates_fract},
                              $sym_atoms[$j]->{coordinates_fract}) < 0.0001 ) {
                    $to_be_deleted{$sym_atoms[$j]->{label}} = 1;
                }
            }
        }
        for my $atom (@sym_atoms) {
            if( !defined $to_be_deleted{$atom->{label}} ) {
                push( @unique_atoms, $atom );
            }
        }
        return @unique_atoms;
    }
}

#===============================================================#
# Generate symmetry equivalents of all atoms from a list, exclude
# duplicates on special positions

sub symgen_all_atoms($$$)
{
    my ( $atoms, $sym_operators, $f2o ) = @_;

    my @sym_atoms = ();

    for my $atom (@{$atoms}) {
        push( @sym_atoms, symgen_atom( $atom, $sym_operators, $f2o ));
    }

    return \@sym_atoms;
}

#===============================================================#
# Shifts a given atom according shifting params. If shifting params are
# (-1, 0, 1) then 27 shifts are made.

# The shift_atom subroutine accepts a reference to a hash
# $atom_info = {label=>"C1_2",
#               chemical_type=>"C",
#               coordinates_fract=>[1.0, 1.0, 1.0],
#               unity_matrix_applied=>1}, returns an array of references of
# above-mentioned hashes
#
sub shift_atom($)
{
    my($atom_info) = @_;

    my @shifted_atoms;
    my @shifting_params = (0, -1, 1);

    my $unit_cell_label;

    if( $atom_info->{"unity_matrix_applied"} ) {
        $unit_cell_label = $atom_info->{label_basename};
    } else {
        $unit_cell_label = $atom_info->{label} . "_555";
    }

    for(my $i = 0; $i < @shifting_params; $i++)
    {
        for(my $j = 0; $j < @shifting_params; $j++)
        {
            for(my $k = 0; $k < @shifting_params; $k++)
            {
                my $new_atom_info = copy_atom($atom_info);
                do {
                    $new_atom_info->{shift} = [ $shifting_params[$i],
                                                $shifting_params[$j],
                                                $shifting_params[$k] ];
                } if 1;
                my @new_atom_xyz;
                if($shifting_params[$i] != 0 || $shifting_params[$j] != 0 ||
                   $shifting_params[$k] != 0 ||
                   $atom_info->{"unity_matrix_applied"} != 1)
                {
                    $new_atom_xyz[0] = $atom_info->{"coordinates_fract"}[0] +
                                                           $shifting_params[$i];
                    $new_atom_xyz[1] = $atom_info->{"coordinates_fract"}[1] +
                                                           $shifting_params[$j];
                    $new_atom_xyz[2] = $atom_info->{"coordinates_fract"}[2] +
                                                           $shifting_params[$k];
                    my $shift_label =
                        ($shifting_params[$i]+5).
                        ($shifting_params[$j]+5).
                        ($shifting_params[$k]+5);
                    $new_atom_info->{"coordinates_fract"} = \@new_atom_xyz;
                    $new_atom_info->{"label"} =
                        $atom_info->{"label"} . "_" . $shift_label;
                    $new_atom_info->{"unit_cell_label"} = $unit_cell_label;
                }
                push(@shifted_atoms, $new_atom_info);
            }
        }
    }

    return @shifted_atoms;
}

#===============================================================#
# Generate symmetry equivalents of an atom, exclude duplicates
# on special positions

sub apply_shifts($)
{
    my ($atoms) = @_;

    my @shifted = ();

    for my $atom (@{$atoms}) {
        push( @shifted, shift_atom( $atom ));
    }

    return @shifted;
}

#===============================================================#
# Calculates distance between two given vectors.

# Accepts two arrays of vectors coordinates_fract.

# Returns a distance.

sub distance
{
    my($vector1, $vector2) = @_;
    my $dist = 0;

    for(my $k = 0; $k < @{$vector1}; $k++)
    {
        $dist += (${$vector1}[$k] - ${$vector2}[$k])**2;
    }
    return sqrt($dist);
}

#===============================================================#
# Tests if a symmetry operator is   (1, 0, 0)
#                                   (0, 1, 0)
#                                   (0, 0, 1)

sub symop_is_unity
{
    my($symop) = @_;
    my $eps = 1e-10;

    for(my $i = 0; $i < @{$symop}; $i++)
    {
        for(my $j = 0; $j < @{$symop}; $j++)
        {
            if($i == $j)
            {
                if(abs(${$symop}[$i][$j] - 1) > $eps) {
                    return 0;
                }
            }
            else
            {
                if(abs(${$symop}[$i][$j] - 0) > $eps) {
                    return 0;
                }
            }
        }
    }
    return 1;
}

#===============================================================#
# Renames CIF data tags so that they are not confused with the
# original ones.

# Accepts a dataset hash produced by CIFParser, a list of tags to be
# renamed, and a prefix to be appended

# Returns a hash with renamed data tags

sub rename_tags($$$)
{
    my ($dataset, $tags2rename, $prefix ) = @_;

    my $values = $dataset->{values};
    my %renamed_tags = ();

    for my $tag (@$tags2rename) {
        if( exists $values->{$tag} &&
            !defined $dataset->{inloop}{$tag} ) {
            my $new_tag = $prefix . $tag;
            my $value = $values->{$tag}[0];
            set_tag( $dataset, $new_tag, $value );
            $renamed_tags{$new_tag} = $tag;
        }
    }
    return wantarray ? %renamed_tags : \%renamed_tags;
}

#===============================================================#
# Prints molecule to the CIF file.

# Accepts a hash
# %molecule = (
#               atoms_c=>[\%atom_info1, \%atom_info2], #covalent bond
#               chemical_formula_sum=>"\\'C6 H6\\'",
#             );

sub print_molecule
{
    my( $molecule_id, $audit, $molecule, $Id, $dataset, $dataset_name,
        $filename ) = @_;

    $dataset->{"name"} = $dataset_name . "_molecule_" . $molecule_id;

    my @data2copy = qw(
    _publ_author_name
    _publ_section_title
    _journal_issue
    _journal_name_full
    _journal_page_first
    _journal_page_last
    _journal_volume
    _journal_year

    _cell_length_a
    _cell_length_b
    _cell_length_c
    _cell_angle_alpha
    _cell_angle_beta
    _cell_angle_gamma

    _cell_measurement_pressure
    _cell_measurement.pressure
    _cell_measurement.pressure_esd
    _cell_measurement_pressure_gPa
    _cell_measurement_radiation
    _cell_measurement.radiation
    _cell_measurement.temp
    _cell_measurement_temperature
    _cell_measurement_temperature_C
    _cell_measurement.temp_esd
    _cell_measurement_wavelength
    _cell_measurement.wavelength
    _cell_measurement_wavelength_nm
    _cell_measurement_wavelength_pm

    _diffrn_ambient_environment
    _diffrn.ambient_environment
    _diffrn_ambient_pressure
    _diffrn.ambient_pressure
    _diffrn.ambient_pressure_esd
    _diffrn_ambient_pressure_gPa
    _diffrn_ambient_pressure_gt
    _diffrn.ambient_pressure_gt
    _diffrn_ambient_pressure_lt
    _diffrn.ambient_pressure_lt
    _diffrn.ambient_temp
    _diffrn.ambient_temp_details
    _diffrn_ambient_temperature
    _diffrn_ambient_temperature_C
    _diffrn_ambient_temperature_gt
    _diffrn_ambient_temperature_lt
    _diffrn.ambient_temp_esd
    _diffrn.ambient_temp_gt
    _diffrn.ambient_temp_lt

    _diffrn_radiation_collimation
    _diffrn_radiation.collimation
    _diffrn_radiation_detector
    _diffrn_radiation_detector_dtime
    _diffrn_radiation.diffrn_id
    _diffrn_radiation.div_x_source
    _diffrn_radiation.div_x_y_source
    _diffrn_radiation.div_y_source
    _diffrn_radiation_filter_edge
    _diffrn_radiation.filter_edge
    _diffrn_radiation_filter_edge_nm
    _diffrn_radiation_filter_edge_pm
    _diffrn_radiation_inhomogeneity
    _diffrn_radiation.inhomogeneity
    _diffrn_radiation_monochromator
    _diffrn_radiation.monochromator
    _diffrn_radiation_polarisn_norm
    _diffrn_radiation.polarisn_norm
    _diffrn_radiation_polarisn_ratio
    _diffrn_radiation.polarisn_ratio
    _diffrn_radiation.polarizn_source_norm
    _diffrn_radiation.polarizn_source_ratio
    _diffrn_radiation_probe
    _diffrn_radiation.probe
    _diffrn_radiation_source
    _diffrn_radiation_type
    _diffrn_radiation.type
    _diffrn_radiation_wavelength
    _diffrn_radiation_wavelength_id
    _diffrn_radiation_wavelength.id
    _diffrn_radiation.wavelength_id
    _diffrn_radiation_wavelength_nm
    _diffrn_radiation_wavelength_pm
    _diffrn_radiation_wavelength.wavelength
    _diffrn_radiation_wavelength_wt
    _diffrn_radiation_wavelength.wt
    _diffrn_radiation_xray_symbol
    _diffrn_radiation.xray_symbol

    _diffrn_reflns_theta_full
    _diffrn_reflns_resolution_full
    _diffrn_reflns_theta_max
    _diffrn_reflns_resolution_max
    _reflns_d_resolution_high
    _reflns.d_resolution_high
    _reflns_d_resolution_high_nm
    _reflns_d_resolution_high_pm
    _reflns_d_resolution_low
    _reflns.d_resolution_low
    _reflns_d_resolution_low_nm
    _reflns_d_resolution_low_pm
    _diffrn_reflns_limit_h_max
    _diffrn_reflns.limit_h_max
    _diffrn_reflns_limit_h_min
    _diffrn_reflns.limit_h_min
    _diffrn_reflns_limit_k_max
    _diffrn_reflns.limit_k_max
    _diffrn_reflns_limit_k_min
    _diffrn_reflns.limit_k_min
    _diffrn_reflns_limit_l_max
    _diffrn_reflns.limit_l_max
    _diffrn_reflns_limit_l_min
    _diffrn_reflns.limit_l_min
);

    my @data2rename = qw(
    _chemical_formula_analytical
    _chemical_formula.analytical
    _chemical_formula.entry_id
    _chemical_formula_iupac
    _chemical_formula.iupac
    _chemical_formula_moiety
    _chemical_formula.moiety
    _chemical_formula_structural
    _chemical_formula.structural
    _chemical_formula_sum
    _chemical_formula.sum
    _pd_proc_ls_prof_R_factor
    _pd_proc_ls_prof_wR_factor
    _refine_hist.R_factor_all
    _refine_hist.R_factor_obs
    _refine_hist.R_factor_R_free
    _refine_hist.R_factor_R_work
    _refine_ls_class_R_factor_all
    _refine_ls_class.R_factor_all
    _refine_ls_class_R_factor_gt
    _refine_ls_class.R_factor_gt
    _refine_ls_class_wR_factor_all
    _refine_ls_class.wR_factor_all
    _refine_ls_R_factor_all
    _refine.ls_R_factor_all
    _refine_ls_R_factor_gt
    _refine.ls_R_factor_gt
    _refine_ls_R_factor_obs
    _refine.ls_R_factor_obs
    _refine.ls_R_factor_R_free
    _refine.ls_R_factor_R_free_error
    _refine.ls_R_factor_R_free_error_details
    _refine.ls_R_factor_R_work
    _refine_ls_shell.R_factor_all
    _refine_ls_shell.R_factor_obs
    _refine_ls_shell.R_factor_R_free
    _refine_ls_shell.R_factor_R_free_error
    _refine_ls_shell.R_factor_R_work
    _refine_ls_shell.wR_factor_all
    _refine_ls_shell.wR_factor_obs
    _refine_ls_shell.wR_factor_R_free
    _refine_ls_shell.wR_factor_R_work
    _refine_ls_wR_factor_all
    _refine.ls_wR_factor_all
    _refine_ls_wR_factor_gt
    _refine_ls_wR_factor_obs
    _refine.ls_wR_factor_obs
    _refine_ls_wR_factor_ref
    _refine.ls_wR_factor_R_free
    _refine.ls_wR_factor_R_work
    _reflns_class_R_factor_all
    _reflns_class.R_factor_all
    _reflns_class_R_factor_gt
    _reflns_class.R_factor_gt
    _reflns_class_wR_factor_all
    _reflns_class.wR_factor_all
);

    my %data2copy = map { $_, $_ } @data2copy;

    my @tag_list = @{$dataset->{tags}};

    my $atom_site_type_symbol = $dataset->{values}{_atom_site_type_symbol};

    my %renamed_tags = rename_tags( $dataset, \@data2rename, "_cod_src" );

    my %known_tags = ( %data2copy, %renamed_tags );
    my @known_tags = ( @data2copy, keys %renamed_tags );

    print_cif( $dataset,
                        {
                            dictionary_tags => \%known_tags,
                            dictionary_tag_list => \@known_tags,
                            exclude_misspelled_tags => 1,
                        } );

    my @data2generate = qw( _audit_creation_method
                            _chemical_formula_sum
                            _cod_data_source_file
                            _cod_data_source_block
    );

    foreach my $data_tag (@data2generate)
    {
        if ( $data_tag eq "_audit_creation_method" )
        {
            if ( $audit == 1 )
            {
                my $id_value = $Id;
                $id_value =~ s/\s*\$\s*//g;
                print_single_tag_and_value( $data_tag, $id_value );
            }
        }
        elsif ( $data_tag eq "_chemical_formula_sum" )
        {
            print_single_tag_and_value( $data_tag,
                                        $molecule->{chemical_formula_sum} );
        }
        elsif ( $data_tag =~ /_cod_data_source_file/ )
        {
            print_single_tag_and_value( $data_tag,
                                        File::Basename::basename( $filename ));
        }
        elsif ( $data_tag =~ /_cod_data_source_block/ )
        {
            print_single_tag_and_value( $data_tag, $dataset_name );
        }
        else
        {
            die( "do not know how to generate tag '$data_tag'" );
        }

    }

    print "_symmetry_space_group_name_H-M   \'P 1\'\n";
    print "loop_ _symmetry_equiv_pos_as_xyz \'x, y, z\'\n";
    print "loop_\n";
    print "_atom_site_label\n";
    print "_atom_site_type_symbol\n"
        if defined $atom_site_type_symbol;
    print "_atom_site_fract_x\n";
    print "_atom_site_fract_y\n";
    print "_atom_site_fract_z\n";

    my $print_format = join( " ", ($format)x3 ) . "\n";
    foreach my $atom ( sort {
        length($a->{label}) == length($b->{label}) ?
            $a->{label} cmp $b->{label} :
            length($a->{label}) <=> length($b->{label})
        } @{$molecule->{atoms_c}} )
    {
        CIFTagPrint::print_value( $atom->{"label"} );
        print " ";
        if( defined $atom_site_type_symbol ) {
            if( defined $atom->{atom_site_type_symbol} ) {
                CIFTagPrint::print_value( $atom->{atom_site_type_symbol} );
                print " ";
            } else {
                print "? ";
            }
        }
        printf $print_format, $atom->{"coordinates_fract"}[0],
               $atom->{"coordinates_fract"}[1], $atom->{"coordinates_fract"}[2];
    }
}

#===============================================================#
# Finds all possible molecules in the CIF file. If two atoms are connected via
# then the algorithm states that there in no bond between these two atoms.

# The algorithm:
# 1. Takes an initial atom and tests if it has not been found in the other
#    molecule yet
# 2. If not, then begins to search for the other molecule:
# 2.1  Does modulo_1 for the initial atom
# 2.2  Finds a translation from initial atom to atom_modulo_1
# 2.3  Searches for all neighbors of atom_modulo_1
# 2.4  For each neighbor of atom_modulo_1 does 2.1 -- 2.4
# 2.5  atom_modulo_1 and all its neigbors translates according translation
#       vector. atom_modulo_1 now becomes initial atom. The others - accordingly
# 3. Stops and does the step 1 until there is no left any initial atom.


# Accepts
#     covalent_sensitivity - a threshold for covalent sensitivity
#     atom_properties(
#           H => {
#                     name => Hydrogen, #(chemical_type)
#                     period => 1,
#                     group => 1,
#                     block => s,
#                     atomic_number => "1",
#                     atomic_weight => 1.008,
#                     covalent_radius => 0.23,
#                     vdw_radius => 1.09,
#                     valency => [1],
#                     },
#          );
# symmetric_atoms and initial_atoms are arrays of
#                                 $atom_info = {
#                                             label=>"C1_2",
#                                             chemical_type=>"C",
#                                             coordinates_fract=>[1.0, 1.0,1.0],
#                                             coordinates_ortho=>[1.0, 1.0,1.0],
#                                             unity_matrix_applied=>1
#                                             }
# Returns an array of
# %molecule = (
#               atoms_c=>[\%atom_info1, \%atom_info2], #covalent bond
#               chemical_formula_sum=>"C6 H6",
#             );

sub find_molecules($$$$$$$$$)
{
    my $covalent_sensitivity = shift(@_);
    my $atom_properties      = shift(@_);
    my $symmetric_atoms      = shift(@_);
    my $initial_atoms        = shift(@_);
    my $bricks               = shift(@_);
    my $filename             = shift(@_);
    my $dataname             = shift(@_);
    my $seen_molecules       = shift(@_);
    my $ortho_matrix         = shift(@_);

    my @unique_molecules;
    my %used_atoms;
    my %used_bonds;
    my %used_basenames;

    foreach my $initial_atom (@$initial_atoms)
    {
        if ((not exists $used_basenames{$initial_atom->{label_basename}}))
        {
            my @molecule_atoms = find_molecule( $covalent_sensitivity,
                                                $atom_properties,
                                                $symmetric_atoms,
                                                \%used_atoms,
                                                \%used_basenames,
                                                \%used_bonds,
                                                $initial_atom, $bricks,
                                                $filename, $dataname,
                                                $ortho_matrix );

            if(@molecule_atoms > 0)
            {
                my $signature;

                if( $uniquify_molecules ) {
                    $signature = join( " ", sort {$a cmp $b} 
                                       map {$_->{label}} @molecule_atoms );
                }

                if( !$uniquify_molecules || !$seen_molecules->{$signature} ) {

                    my %molecule =  (atoms_c              => \@molecule_atoms,
                                     chemical_formula_sum => "",);

                    push( @unique_molecules, \%molecule );

                    $seen_molecules->{$signature} = 1 if $uniquify_molecules ;
                }
            }
        }
    }

    return @unique_molecules;
}

# ============================================================================ #

sub find_molecule($$$$$$$$$$$)
{
    my $covalent_sensitivity = shift(@_);
    my $atom_properties      = shift(@_);
    my $symmetric_atoms      = shift(@_);
    my $used_atoms           = shift(@_);
    my $used_basenames       = shift(@_);
    my $used_bonds           = shift(@_);
    my $current_atom         = shift(@_);
    my $bricks               = shift(@_);
    my $filename             = shift(@_);
    my $dataname             = shift(@_);
    my $ortho_matrix         = shift(@_);

    my @current_coords_fract_modulo_1 =
        map { modulo_1($_) } @{$current_atom->{coordinates_fract}};

    my $atom_in_unit_cell_coords_ortho =
	multiply_matrix_with_vector( $ortho_matrix,
				     \@current_coords_fract_modulo_1 );

    my $current_translation = translation( $current_atom->{coordinates_fract},
                                           \@current_coords_fract_modulo_1 );

    my $new_atom = copy_atom($current_atom);

    if( exists $new_atom->{unit_cell_label} ) {
        $new_atom->{label} = $new_atom->{unit_cell_label};
    }

    my @neighbors;

    if(exists $used_atoms->{$new_atom->{label}})
    {
        print "<<<< atom labeled '$new_atom->{label}' " .
            "is already in some molecule, returning\n"
            if $debug;
        return @neighbors;
    }

    $used_atoms->{$new_atom->{label}} = $new_atom->{label};
    $used_atoms->{$current_atom->{label}} = $current_atom->{label};

    $used_basenames->{$current_atom->{label_basename}} =
	$current_atom->{label_basename};

    push( @neighbors, $new_atom );

    my ($i, $j, $k) =
        AtomBricks::get_atom_index( $bricks,
                                    @{$atom_in_unit_cell_coords_ortho});

    my ( $min_i, $max_i, $min_j, $max_j, $min_k, $max_k );

    eval {
        ( $min_i, $max_i, $min_j, $max_j, $min_k, $max_k ) =
            AtomBricks::get_search_span( $bricks, $i, $j, $k );
    };
    if( $@ ) {
        use Serialise;
        serialiseRef( $atom_in_unit_cell_coords_ortho );
        serialiseRef( [ $i, $j, $k ] );
        delete $bricks->{atoms};
        serialiseRef( $bricks );
        die( $@ );
    }
                                  
    ## foreach my $sym_atom (@$symmetric_atoms)
    for $i ($min_i .. $max_i) {
    for $j ($min_j .. $max_j) {
    for $k ($min_k .. $max_k) {
        for my $sym_atom ( @{$bricks->{atoms}[$i][$j][$k]} ) {
            my $coords_ortho    = $sym_atom->{coordinates_ortho};
            my $label           = $sym_atom->{label};
            my $label_basename  = $sym_atom->{label_basename};

            if( not exists $used_bonds->{$label}{$new_atom->{label}} )
            {

                $used_bonds->{$label} = {}
                    unless exists $used_bonds->{$label};
                $used_bonds->{$label}{$new_atom->{label}} = 1;

                $used_bonds->{$new_atom->{label}} = {}
                     unless exists $used_bonds->{$new_atom->{label}};
                $used_bonds->{$new_atom->{label}}{$label} = 1;

                my $dist = distance( $atom_in_unit_cell_coords_ortho,
                                     $coords_ortho );

                do {
                    local $" = ' ';
                    print ">>> checking $new_atom->{label} " .
                        "(@{$atom_in_unit_cell_coords_ortho}) vs. " .
                        "$label (@{$coords_ortho}), d = $dist\n";
                } if $debug;

                my $is_bump = test_bump( $atom_properties,
					 $current_atom->{chemical_type},
					 $sym_atom->{chemical_type},
                                         $new_atom->{label_basename},
                                         $label_basename,
                                         $dist, $bump_distance_factor );

                if( $is_bump ) {
                    my $message =  "atoms \"$new_atom->{label}\" and " .
                        "\"$label\" are too close " .
                        "(distance = " .
                        sprintf( "%6.4f", $dist ) .
                        ") and are considered a bump";
                    if( $ignore_bumps ) {
                        warning( $0, $filename, $dataname, $message );
                    } else {
                        error( $0, $filename, $dataname, $message );
                        print STDERR "$0: Aborting calculations.\n";
                        exit( 254 );
                    }
                }

                my $is_bond = test_bond($atom_properties,
					$current_atom->{chemical_type},
                                        $sym_atom->{"chemical_type"},
                                        $dist,
                                        $covalent_sensitivity);

                if($is_bond == 1)
                {
                    do {
                        use Serialise;
                        local $" = ' ';
                        print ">>> found bond:\n";
                        serialiseRef( { "translation" => $current_translation,
                                        "original atom" => $current_atom,
                                        "atom" => $sym_atom } );
                    } if $debug;

                    my $back_shifted_sym_atom =
                        translate_atom( $sym_atom, $current_translation );
                    push(@neighbors, find_molecule( $covalent_sensitivity,
                                                    $atom_properties,
                                                    $symmetric_atoms,
                                                    $used_atoms,
                                                    $used_basenames,
                                                    $used_bonds,
                                                    $back_shifted_sym_atom,
                                                    $bricks,
                                                    $filename, $dataname,
                                                    $ortho_matrix ));
                }
            }
        }
    }}}

    print ">>> Finished checks;\n" if $debug;

    do {
        use Serialise;
        print ">>> Before translation:";
        serialiseRef( \@neighbors );
    } if $debug;

    ## my $translated_neighbors = translate_atoms(\@neighbors,
    ##                                            $current_translation);
    ## 
    ## do {
    ##     use Serialise;
    ##     print ">>> After translation:";
    ##     serialiseRef( $translated_neighbors );
    ## } if $debug;

    return @neighbors;
}

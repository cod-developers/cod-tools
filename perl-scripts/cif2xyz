#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Read CIF files and print atom coordinates in XYZ format.
#**

use strict;
use warnings;
use AtomProperties;
use CIFParser;
use CCIFParser;
use Fractional;
use SOptions;
use SUsage;
use UserMessage;

my $print_radii = 0;
my $use_parser = "c"; # Used CIF parser

@ARGV = getOptions(
#* Usage:
#*     $0 --options input.cif
#*
#* Options:
#*     --print-radii
#*         Append covalent radii to the atom coordinates.
#*
#*     --no-print-radii, --dont-print-radii, --xyz-only
#*         Print only Cartesian XYZ coordinates for input atoms
#*         (default behaviour)
#*
#*     --use-perl-parser
#*     --use-c-parser
#*         Specify parser to parse CIF files. C parser is default.
#*
#*     --help, --usage
#*         Print a short usage message (this message) and exit.
#**
    "--print-radii" => sub{ $print_radii = 1 },
    "--no-print-radii,--dont-print-radii" => sub{ $print_radii = 0 },
    "--xyz-only" => sub{ $print_radii = 0 },

    "--use-perl-parser" => sub{ $use_parser = "perl" },
    "--use-c-parser"    => sub{ $use_parser = "c" },

    "--help,--usage" => sub { SUsage::usage; exit },
);

@ARGV = ("-") unless @ARGV;

for my $filename (@ARGV) {

    my( $data, $nerrors );
    if( $use_parser eq "perl" ) {
        my $parser = new CIFParser;
        $data = $parser->Run($filename);
        if( defined $parser->YYData->{ERRCOUNT} ) {
            $nerrors = $parser->YYData->{ERRCOUNT};
        }            
    } else {
        ( $data, $nerrors ) = CCIFParser::parse($filename);
    }

    if( defined $nerrors && $nerrors > 0 ) {
        error( $0, $filename, undef,
               "$nerrors error(s) encountered while " .
               "parsing file '$filename'" );
        exit(255);
    }

    if( !@{$data} || !defined $data->[0] || !defined $data->[0]{name} ) {
        warning( $0, $filename, undef, "file '$filename' seems to be empty" );
        next;
    }

    for my $dataset (@$data) {
        my $values = $dataset->{values};

        my @cell = get_unit_cell( $values, $filename, $dataset->{name} );

        my $f2o = symop_ortho_from_fract(@cell);
        # perform fractional to orthogonal conversion here

        my $atom_site_tag;

        if( exists $values->{"_atom_site_label"} ) {
            $atom_site_tag = "_atom_site_label";
        } elsif( exists $values->{"_atom_site_type_symbol"} ) {
            $atom_site_tag = "_atom_site_type_symbol";

            error( $0, $filename, $dataset->{name},
                   "_atom_site_label tag was not found. " .
                   "A serial number will be appended " .
                   "to _atom_site_type_symbol to make atom labels" );
        } else {
            error( $0, $filename, $dataset->{name},
                   "neither _atom_site_type_symbol nor _atom_site_label " .
                   "were found" );
            return undef;
        }

        my $atom_labels = $values->{$atom_site_tag};

        for (my $i = 0; $i < @{$atom_labels}; $i++) {
            my $covalent_radius;
            if( $print_radii ) {
                my $atom_type;
                if( exists $values->{_atom_site_type_symbol} &&
                    defined $values->{_atom_site_type_symbol}[$i] &&
                    $values->{_atom_site_type_symbol}[$i] ne '?' ) {
                    $atom_type = $values->{_atom_site_type_symbol}[$i];
                    if( $atom_type =~ m/^([A-Za-z]{1,2})/ ) {
                        $atom_type = ucfirst( lc( $1 ));
                    }
                } else {
                    if( $values->{_atom_site_label}[$i] =~
                        m/^([A-Za-z]{1,2})/ ) {
                        $atom_type = ucfirst( lc( $1 ));
                    } else {
                        die( "could not determine atom type for atom " .
                             "'$values->{_atom_site_label}[$i]'" );
                    }
                }
                if( !exists $AtomProperties::atoms{$atom_type} ) {
                    die( "unknown atom type: '$atom_type'" );
                }
                $covalent_radius =
                    $AtomProperties::atoms{$atom_type}->{covalent_radius};
            }

            my @atom_xyz;
            for my $cif_fract ( "_atom_site_fract_x",
                                "_atom_site_fract_y",
                                "_atom_site_fract_z",)
            {
                push( @atom_xyz, $values->{$cif_fract}[$i] );
                $atom_xyz[-1] =~ s/\(\d+\)$//;
            }
            my @coordinates_ortho = mat_vect_mul( $f2o, \@atom_xyz );

            print $coordinates_ortho[0], " ",
                  $coordinates_ortho[1], " ",
                  $coordinates_ortho[2], " ",
                  ($print_radii ? "$covalent_radius " : ""),
                  '# ', $atom_labels->[$i], "\n";
        }
    }
}

#===============================================================#
# Extract unit cell angles and lengths.
#
# Accepts
#     values - a hash where a data from the CIF file is stored
# Returns
#     cell - an array  with stored information.

sub get_unit_cell
{
    my( $values, $filename, $dataname ) = @_;

    my @cell;

    for my $cif_tag (qw(_cell_length_a
                        _cell_length_b
                        _cell_length_c
                        ))
    {
        push(@cell, $values->{$cif_tag}[0]);
        $cell[-1] =~ s/\(\d+\)$//;
    }

    for my $cif_tag (qw(_cell_angle_alpha
                        _cell_angle_beta
                        _cell_angle_gamma
                        ))
    {
        if( exists $values->{$cif_tag} ) {
            push( @cell, $values->{$cif_tag}[0] );
            $cell[-1] =~ s/\(\d+\)$//;
        } else {
            warning( $0, $filename, $dataname,
                     "cell angle '$cif_tag' not present -- " .
                     "taking default value 90 degress." );
            push( @cell, 90 );
        }
    }

    return @cell;
}

#===============================================================#
# Multiplies an ortho matrix with a vector.

# my $ortho = [
#     [ o11 o12 o13 ]
#     [ 0   o22 o23 ]
#     [ 0   0   o33 ]
# ]
#

sub mat_vect_mul
{
    my($matrix, $vector) = @_;

    my @new_coordinates;

    for(my $i = 0; $i < @{$vector}; $i++)
    {
        $new_coordinates[$i] = 0;
        for(my $j = 0; $j < @{$vector}; $j++)
        {
            $new_coordinates[$i] += ${$matrix}[$i][$j] * ${$vector}[$j];
        }
    }

    if( @$vector == 3 && @$matrix == 4 ) {
        $new_coordinates[0] += $matrix->[0][3];
        $new_coordinates[1] += $matrix->[1][3];
        $new_coordinates[2] += $matrix->[2][3];
    }

    return wantarray ? @new_coordinates : \@new_coordinates;
}

#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Correct misspelled tags in a CIF file. 
#**

use strict;
use lib "./lib/perl5";
use lib "./CIFParser";
use CIFParser;
use CIFTagCanonicalNames;
use CIFDictTags;
use CIFCODTags;
use CIFTagManage;
use CIFTagPrint;
use SOptions;
use SUsage;

my $keep_tag_order = 0;

my %misspelled_tags = (
    '_atom_site_symmetry_multiplicity' => [ '_atom_site_symetry_multiplicity' ],
    '_atom_site_U_iso_or_equiv' => [ '_atom_site_u_iso' ],
    '_diffrn_radiation_monochromator' => [ '_diffrn_radiation_monochromador' ],
    ''
);

my %tag_spelling = ();

while( my ($correct, $all_misspelled) = each %misspelled_tags ) {
    for my $misspelled (@{$all_misspelled}) {
	$tag_spelling{$misspelled} = $correct;
    }
}

@ARGV = getOptions(
    "--keep-tag-order" => sub { $keep_tag_order = 1; },
    "--sort-tags"      => sub { $keep_tag_order = 0; },
    "--help,--usage"   => sub { SUsage::usage; exit },
);

my @dictionary_tags = ( @CIFDictTags::tag_list, @CIFCODTags::tag_list );
my %dictionary_tags = map { $_, $_ } @dictionary_tags;

@ARGV = ("-") unless @ARGV;

for my $filename (@ARGV) {
    my $parser = new CIFParser;
    my $data = $parser->Run($filename);

    if( defined $parser->YYData->{ERRCOUNT} &&
        $parser->YYData->{ERRCOUNT} > 0 ) {
	print STDERR "$0: $filename: ",
	$parser->YYData->{ERRCOUNT},
	" error(s) encountered while parsing CIF data\n";
	exit -1;
    }

    canonicalize_all_names( $data );

    for my $dataset (@$data) {

	my $tags = $dataset->{tags};
	my $values = $dataset->{values};

	for my $tag (@$tags) {
	    if( exists $tag_spelling{$tag} ) {
		my $correct = $tag_spelling{$tag};
		if( !exists $values->{$correct} ) {
		    CIFTagManage::rename_tag( $dataset, $tag, $correct );
		}
	    }
	    if( $tag =~ /^_+(_[^_].*)$/ ) {
		my $correct = $1;
		if( exists $dictionary_tags{$correct} &&
		    !exists $values->{$correct} ) {
		    CIFTagManage::rename_tag( $dataset, $tag, $correct );
		}
	    }
	}

	print_cif( $dataset, {
	    exclude_misspelled_tags => 0,
	    preserve_loop_order => 1,
	    fold_long_fields => 0,
	    dictionary_tags => \%dictionary_tags,
	    dictionary_tag_list => \@dictionary_tags,
	    keep_tag_order => $keep_tag_order,
	} );
    }
}

#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl5 -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Parse a CIF file and print out the essential information in the COD
#  REF format
#**

use strict;
use lib "./lib/perl5";
use STAR::Parser;
use SOptions;
use SUsage;

my $user_biblio = 0;
my $leave_biblio = 0;

my $journal;
my $volume;
my $start_page;
my $end_page;
my $year;

@ARGV = getOptions( 
    "-j,--journal"   => sub{ $user_biblio = 1; $journal = get_value() },
    "-v,--volume"    => sub{ $user_biblio = 1; $volume = get_value() },
    "-p,--page"      => sub{ $user_biblio = 1; $start_page = get_value() },
    "--start-page"   => sub{ $user_biblio = 1; $start_page = get_value() },
    "-e,--end-page"  => sub{ $user_biblio = 1; $end_page = get_value() },
    "-y,--year"      => sub{ $user_biblio = 1; $year = get_value() },
    "--leave-bibliography"   => sub{ $leave_biblio = 1 },
    "--discard-bibliography" => sub{ $leave_biblio = 0 },
    "--help,--usage" => sub { SUsage::usage; exit },
);

my $filename = shift(@ARGV);
my $biblio = @ARGV > 0 ? shift(@ARGV) : undef;
my @data;

@data = STAR::Parser->parse(-file=>$filename);

my @requested_cif_tags = (
    "_chemical_formula_sum",
    "_chemical_name_systematic",
    "_journal_name_full",
    "_journal_year",
    "_journal_volume",
    "_journal_page_first",
    "_journal_page_last",
    "_cell_measurement_temperature",
    "_diffrn_ambient_temperature",
    "_cell_length_a",
    "_cell_length_b",
    "_cell_length_c",
    "_cell_angle_alpha",
    "_cell_angle_beta",
    "_cell_angle_gamma",
    "_symmetry_space_group_name_H-M",
    "_symmetry_equiv_pos_as_xyz",
    "_atom_site_B_iso_or_equiv",
    #
    # Data collection parameters:
    #
    "_diffrn_radiation_type",
    "_diffrn_radiation_wavelength",
    "_diffrn_reflns_number",
    "_diffrn_reflns_av_R_equivalents",
    "_diffrn_reflns_av_sigmaI/netI",
    "_diffrn_reflns_limit_h_min",
    "_diffrn_reflns_limit_h_max",
    "_diffrn_reflns_limit_k_min",
    "_diffrn_reflns_limit_k_max",
    "_diffrn_reflns_limit_l_min",
    "_diffrn_reflns_limit_l_max",
    "_diffrn_reflns_theta_min",
    "_diffrn_reflns_theta_max",
    "_reflns_number_total",
    "_reflns_number_gt",
    "_reflns_threshold_expression",
    #
    # Refinement statistics:
    #
    "_refine_ls_number_reflns",
    "_refine_ls_number_parameters",
    "_refine_ls_number_restraints",
    "_refine_ls_R_factor_all",
    "_refine_ls_R_factor_gt",
    "_refine_ls_wR_factor_ref",
    "_refine_ls_wR_factor_gt",
);

my $n = 0;

for my $dataset (@data) {

    die unless defined $dataset->{DATA};

    my $compounds = $dataset->{DATA};
    my @names = keys %{$compounds};

    ## print int(@data), "\n"; exit;

    my $compound_name = $names[0];
    my $compound = $compounds->{$compound_name};
    my $category = defined $compound ? $compound->{"-"} : undef;
    my $datablok = defined $category ? $category->{"-"} : undef;

    next if !defined $datablok or !defined $datablok->{_atom_site_label};

    print "\n" if $n > 0;

    if( $user_biblio ) {
	if( !$leave_biblio ) {
	    for my $key ( grep /_journal_/, keys %{$datablok} ) {
		delete $datablok->{$key};
	    }
	}

	$datablok->{_journal_name_full} = [ $journal ]
	    if defined $journal;

	$datablok->{_journal_year} = [ $year ]
	    if defined $year;

	$datablok->{_journal_volume} = [ $volume ]
	    if defined $volume;

	$datablok->{_journal_page_first} = [ $start_page ]
	    if defined $start_page;

	$datablok->{_journal_page_last} = [ $end_page ]
	    if defined $end_page;
    }

    # Print out requested tags:
    for my $tag (@requested_cif_tags) {
	if( defined $datablok->{$tag} ) {
	    my $data = $datablok->{$tag};
	    if( int(@{$data}) == 1 ) {
		my @lines = split( "\n", $data->[0] );
		for my $line (@lines) {
		    print "#", $tag, "\t", $line, "\n";
		}
	    } else {
		for my $j (0..$#{$data}) {
		    my @lines = split( "\n", $data->[$j] );
		    for my $line (@lines) {
			print "#", $tag, "[", $j, "]", "\t", $line, "\n";
		    }
		}
	    }
	}
    }

    # Chemical formula:
    my $formula = $datablok->{_chemical_formula_sum}[0];
    $formula =~ s/^\s*|\s*$//g;

    # Cell constants:
    my $a = $datablok->{_cell_length_a}[0];
    my $b = $datablok->{_cell_length_b}[0];
    my $c = $datablok->{_cell_length_c}[0];
    my $alpha = $datablok->{_cell_angle_alpha}[0];
    my $beta  = $datablok->{_cell_angle_beta}[0];
    my $gamma = $datablok->{_cell_angle_gamma}[0];

    # Spacegroup:
    my $spacegroup = $datablok->{"_symmetry_space_group_name_H-M"}[0];

    # Atom records:

    my $atom = $datablok->{_atom_site_label};

    my $x = $datablok->{_atom_site_fract_x};
    my $y = $datablok->{_atom_site_fract_y};
    my $z = $datablok->{_atom_site_fract_z};

    my $q = $datablok->{_atom_site_occupancy};
    my $U = $datablok->{_atom_site_U_iso_or_equiv};
    
    if( !defined $U ) {
	$U = $datablok->{_atom_site_Uiso_or_Biso};
    }

    if( !defined $U && defined $datablok->{_atom_site_B_iso_or_equiv} ) {
	my $B = $datablok->{_atom_site_B_iso_or_equiv};
	my $Pi = 3.14159265358979;
	for my $i ( 0..$#{$B} ) {
	    my $b = $B->[$i];
	    $b =~ s/\(.*\)$//;
	    $datablok->{_atom_site_U_iso_or_equiv}[$i] = $b/(8*$Pi**2);
	}
	$U = $datablok->{_atom_site_U_iso_or_equiv};
    }

    my $multiplicity = $datablok->{_atom_site_symmetry_multiplicity};
    my $Wyckoff_sym = $datablok->{_atom_site_Wyckoff_symbol};

    # Anisotripoc temperature factor records:

    my $aniso_label = $datablok->{_atom_site_aniso_label};
    my @anisou = (
	$datablok->{_atom_site_aniso_U_11},
	$datablok->{_atom_site_aniso_U_22},
	$datablok->{_atom_site_aniso_U_33},
	$datablok->{_atom_site_aniso_U_23},
	$datablok->{_atom_site_aniso_U_13},
	$datablok->{_atom_site_aniso_U_12},
    );

    # Print it out:

    if( defined $biblio ) {
	open( BIBLIO, $biblio ) or
	    die( "Could not open file '$biblio' for reading: $!" );
	my $reference;
	if( $biblio =~ /\.ref/ ) {
	    $reference = <BIBLIO>; # read the first line
	} elsif( $biblio =~ /\.xrf/ ) {
	    my @reference = grep !/^\#/, <BIBLIO>;
	    $reference = $reference[0]; # read the first non-comment line
	} else {
	    local $/ = undef; # read the whole file
	    $reference = <BIBLIO>;
	}
	close BIBLIO;
	chomp $reference;
	$reference =~ s/\n/ /g;
	print $reference, "\n";
    } else {
	print "Bibliography ...\n";
    }

    do {
	local $, = " ";
	local $\ = "\n";

	print $formula;

	print $a, $b, $c, $alpha, $beta, $gamma;

	$spacegroup =~ s/\s*//g;
	print $spacegroup;

	for my $i (0..$#{$atom}) {
	    print
		$atom->[$i],
		defined $multiplicity ? $multiplicity->[$i] : "?",
		defined $Wyckoff_sym ? $Wyckoff_sym->[$i] : "?",
		$x->[$i], $y->[$i], $z->[$i],
		$q->[$i], $U->[$i];
	}
    };

    if( defined $aniso_label and int(@{$aniso_label}) > 0 ) {
	for my $i (0..$#{$aniso_label}) {
	    print "ANISOU ", $aniso_label->[$i];
	    for my $anisou (@anisou) {
		print " ", $anisou->[$i];
	    }
	    print "\n";
	}
    }

    $n ++;
}

#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Prepares a calculation tree from descriptions in TCOD CIF file.
#**

use strict;
use warnings;
use Digest::MD5 qw( md5_hex );
use Digest::SHA qw( sha1_hex );
use File::Basename qw( dirname );
use File::Path qw( make_path );
use COD::CIF::Parser qw( parse_cif );
use COD::CIF::Data qw( get_content_encodings );
use COD::CIF::Tags::CanonicalNames qw( canonicalize_all_names );
use COD::Escape qw( decode_textfield );
use COD::SOptions qw( getOptions );
use COD::SUsage qw( usage options );
use COD::UserMessage qw( warning error prefix_dataname );

my $use_parser = 'c';
my $output_dir = '.';
my $dry_run = 0;
my $list = 0;

#* USAGE:
#*     $0 --options input1.cif input2.cif
#*
#* OPTIONS:
#*     -o, --output-dir out/
#*        Base path of the created tree.
#*
#*     --dry-run
#*        Do not create any folders and files, just extract the files and
#*        compare their checksums.
#*
#*     -l, --list
#*        Print the list of created folders and files.
#*
#*     --use-perl-parser
#*        Use Perl parser for CIF parsing.
#*
#*     --use-c-parser
#*        Use Perl & C parser for CIF parsing.
#*
#*     --help, --usage
#*        Print short usage message (this message) and exit.
#**
@ARGV = getOptions(
    "-o,--output-dir" => \$output_dir,

    "--dry-run" => sub{ $dry_run = 1 },

    "-l,--list" => sub{ $list = 1 },

    "--use-perl-parser" => sub { $use_parser = "perl" },
    "--use-c-parser"    => sub { $use_parser = "c" },
    "--options"         => sub { options; exit },
    "--help,--usage"    => sub { usage; exit }
);

@ARGV = ( "-" ) unless @ARGV;

my %created_dirs = ('.' => 1);

for my $filename (@ARGV) {

    my $options = { 'parser' => $use_parser, 'no_print' => 1 };
    my ( $data, $err_count, $messages ) = parse_cif( $filename, $options );

    if ( $err_count > 0 ) {
        print STDERR $_ foreach ( @$messages );
        error( $0, $filename, undef, "$err_count error(s) "
             . "encountered while parsing the file", undef );
        next;
    }
    print STDERR $_ foreach ( @$messages );

    canonicalize_all_names( $data );

    for my $datablock (@$data) {
        my $values   = $datablock->{values};
        my $dataname = prefix_dataname($datablock->{name});

        my $encodings = get_content_encodings( $datablock, $filename );

        for my $i (0..$#{$values->{_tcod_file_id}}) {
            my $path     = $values->{_tcod_file_name}[$i];
            my $dirname  = dirname( $values->{_tcod_file_name}[$i] );
            my $contents = $values->{_tcod_file_contents}[$i];
            my $role     = $values->{_tcod_file_role}[$i];
            my $md5sum   = $values->{_tcod_file_md5sum}[$i];
            my $sha1sum  = $values->{_tcod_file_sha1sum}[$i];
            my $uri      = $values->{_tcod_file_URI}[$i];
            my $encoding;
            if( exists $values->{_tcod_file_content_encoding} ) {
                $encoding = $values->{_tcod_file_content_encoding}[$i];
                $encoding = undef if $encoding eq '.';
            }
            if( !exists $created_dirs{$dirname} ) {
                if( !$dry_run ) {
                    die "can not create path $output_dir/$dirname -- " .
                        "already exists" if -e "$output_dir/$dirname";
                    make_path( "$output_dir/$dirname" );
                }
                print "$output_dir/$dirname\n" if $list;
                $created_dirs{$dirname} = 1;
            }
            next if $path =~ /\/$/; # Skipping folders
            if( !defined $contents || $contents eq '.'
                || $contents eq '?' ) {
                # Contents are not provided
                if( defined $uri && $uri ne '.' && $uri ne '?' ) {
                    # If the URI is given instead of contents, it has to be
                    # fetched
                    use WWW::Curl::Easy;
                    my $curl = WWW::Curl::Easy->new();
                    #$curl->setopt( CURLOPT_HEADER, 1 );
                    $curl->setopt( CURLOPT_URL, $uri );
                    $curl->setopt( CURLOPT_WRITEDATA, \$contents );
                    if( $curl->perform ) {
                        warning( $0, $filename, $dataname,
                                 "unable to fetch URI '$uri': " .
                                 $curl->errbuf, undef );
                    }
                } else {
                    warning( $0, $filename, $dataname,
                             "contents for file '$path' are not " .
                             "provided", 'skipping' );
                    next
                }
            }

            eval {
                if( !$encoding || !$encodings ||
                    !exists $encodings->{$encoding} ) {
                    if( $encoding && $encodings &&
                        !exists $encodings->{$encoding} ) {
                        warning( $0, $filename, $dataname,
                                 "content encoding stack '$encoding' " .
                                 "is not described", "trying to guess" );
                    }
                    # Perform a default decoding, try to guess the encoding
                    # layer type from the encoding ID
                    $contents = decode_textfield( $contents, $encoding );
                } else {
                    for my $layer (reverse @{$encodings->{$encoding}}) {
                        $contents = decode_textfield( $contents, $layer );
                    }
                }
            };
            if( $@ ) {
                warning( $0, $filename, $dataname,
                         "could not decode contents for file '$path'",
                         "$@; will not decode contents" );
                $contents = $values->{_tcod_file_contents}[$i];
            }

            if( $md5sum ) {
                if( md5_hex($contents) ne $md5sum ) {
                    warning( $0, $filename, $dataname,
                             "MD5 checksums of the original '$path' and " .
                             "decoded files are different", undef );
                }
            }
            if( $sha1sum ) {
                if( sha1_hex($contents) ne $sha1sum ) {
                    warning( $0, $filename, $dataname,
                             "SHA1 checksums of the original '$path' and " .
                             "decoded files are different", undef );
                }
            }

            if( !$dry_run ) {
                open( my $out, '>', "$output_dir/$path" );
                print $out $contents;
                close( $out );
                if( $role && $role eq 'script' ) {
                    chmod( 0775, "$output_dir/$path" );
                }
            }
            print "$output_dir/$path\n" if $list;
        }

        print "$output_dir/main.sh\n" if $list;
        if( !$dry_run ) {
            my $main_exec_path = "$output_dir/main.sh";
            open( my $main_fh, '>', $main_exec_path );
            print $main_fh "#!/bin/bash\n\n";

            for my $i (0..$#{$values->{_tcod_computation_step}}) {
                my $env = $values->{_tcod_computation_environment}[$i];
                my $command = $values->{_tcod_computation_command}[$i];
                print $main_fh "(\n";
                if( $env ) {
                    map {print $main_fh "    $_\n" } split( "\n", $env );
                    print $main_fh "\n";
                }
                print $main_fh "    $command\n)\n";
            }
            close( $main_fh );
            chmod( 0775, $main_exec_path );
        }
    }
}

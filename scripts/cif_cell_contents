#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Calculate cell contents from atom coordinates and symmetry information.
#**

use strict;
use warnings;
use COD::CIF::Data::CellContents qw( cif_cell_contents );
use COD::CIF::Parser qw( parse_cif );
use COD::SOptions qw( getOptions );
use COD::SUsage qw( usage options );
use COD::UserMessage qw( error );

my $use_parser = 'c';
my $format = "%g";
my $Z_value;
my $use_attached_hydrogens = 0;
my $assume_full_occupancies = 0;
my $continue_on_errors = 0;
my $print_datablock_name = 0;

#* USAGE:
#*     cif_cell_constants --options input1.cif input*.cif
#*
#* OPTIONS:
#*     -c, --continue-on-errors
#*         Keep processing subsequent data blocks even if one datablock
#*         had fatal processing errors
#*
#*     -c-, --dont-continue-on-errors, --do-not-continue-on-errors,
#*     --no-continue-on-errors
#*         Terminate script immediately if some data block could not be
#*         processed (default).
#*
#*     -Z, --cell-formula-units-Z  1
#*         Specify number of formula sum units in the unit cell.
#*         Default: taken from the input CIF if present, otherwise
#*         assumed to be 1.
#*
#*     --use-attached-hydrogens
#*         Include number of implicit hydrogens, specified using
#*         _atom_site_attached_hydrogens tag, into the formula sum.
#*
#*     --dont-use-attached-hydrogens,
#*     --no-use-attached-hydrogens,
#*     --ignore-attached-hydrogens
#*         Ignore number of implicit hydrogens, specified using
#*         _atom_site_attached_hydrogens tag, in calculation of the
#*         formula sum (default).
#*
#*     --assume-full-occupancies
#*         Assume that all atoms have full (1.0) occupancies.
#*
#*     --dont-assume-full-occupancies,
#*     --no-assume-full-occupancies,
#*     --use-original-occupancies
#*         Use original atom site occupancies, as given in CIF file
#*         (default).
#*
#*     --print-datablock-name
#*         Print datablock name, tab-separated, before each formula.
#*
#*     --dont-print-datablock-name,
#*     --do-not-print-datablock-name,
#*     --no-print-datablock-name
#*         Do not print datablock name (default).
#*
#*     --use-perl-parser
#*         Use Perl parser to parse CIF files.
#*     --use-c-parser
#*         Use C parser to parse CIF files (default).
#*
#*     --help, --usage
#*         Print a short usage message (this message) and exit.
#**
@ARGV = getOptions(
    "-c,--continue-on-errors" => sub { $continue_on_errors = 1 },
    "-c-,--dont-continue-on-errors" => sub { $continue_on_errors = 0 },
    "--do-not-continue-on-errors" => sub { $continue_on_errors = 0 },
    "--no-continue-on-errors" => sub { $continue_on_errors = 0 },

    "-Z,--cell-formula-units-Z" => \$Z_value,

    "--use-attached-hydrogens" => sub { $use_attached_hydrogens = 1 },
    "--dont-use-attached-hydrogens" => sub { $use_attached_hydrogens = 0 },
    "--no-use-attached-hydrogens" => sub { $use_attached_hydrogens = 0 },
    "--ignore-attached-hydrogens" => sub { $use_attached_hydrogens = 0 },

    "--assume-full-occupancies" => sub { $assume_full_occupancies = 1 },
    "--dont-assume-full-occupancies" => sub { $assume_full_occupancies = 0 },
    "--no-assume-full-occupancies" => sub { $assume_full_occupancies = 0 },
    "--use-original-occupancies" => sub { $assume_full_occupancies = 0 },

    "--print-datablock-name" => sub{ $print_datablock_name = 1 },
    "--no-print-datablock-name" => sub{ $print_datablock_name = 0 },
    "--dont-print-datablock-name" => sub{ $print_datablock_name = 0 },
    "--do-not-print-datablock-name" => sub{ $print_datablock_name = 0 },

    "--use-perl-parser" => sub { $use_parser = "perl" },
    "--use-c-parser"    => sub { $use_parser = "c" },
    "--options"         => sub { options; exit },
    "--help,--usage"    => sub { usage; exit }
);

my $Id = '$Id$';

@ARGV = ( "-" ) unless @ARGV;

for my $filename (@ARGV) {
    my $options = { 'parser' => $use_parser, 'no_print' => 1 };
    my ( $data, $err_count, $messages ) = parse_cif( $filename, $options );

    if ( $err_count > 0 ) {
        print STDERR $_ foreach ( @$messages );
        error( $0, $filename, undef, "$err_count error(s) "
             . "encountered while parsing the file", undef );
        next;
    }
    print STDERR $_ foreach ( @$messages );

    for my $dataset (@$data) {

        my $values = $dataset->{values};

        my $formula;
        eval {
            $formula = cif_cell_contents
                ( $dataset,  $filename, $Z_value,
                  $use_attached_hydrogens, $assume_full_occupancies );
        };
        if( $@ ) {
            if( $continue_on_errors ) {
                $@ =~ s/^Died /... /;
                print STDERR $@;
                next;
            } else {
                die $@;
            }
        }

        if( defined $formula ) {
            if( $print_datablock_name ) {
                print $dataset->{name}, "\t";
            }
            print $formula, "\n";
        }
    }
}

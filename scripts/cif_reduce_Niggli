#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Performs Niggli cell reduction of CIF files.
#**

use strict;
use warnings;
use COD::CIFParser::CIFParser;
use COD::CIF::Data qw(get_cell);
use COD::CIF::Tags::TagCanonicalNames;
use COD::CIF::Tags::TagPrint;
use COD::Cell::Niggli::KG76 qw(reduce);
use COD::Fractional;
use COD::UserMessage;
use COD::SOptions;
use COD::SUsage;
use COD::Spacegroups::SymopAlgebra qw(symop_transpose
                                      symop_mul);

sub cholesky($);

my $use_parser = 'c';
$COD::Cell::Niggli::KG76::debug = 0;

my $Pi = 4 * atan2(1,1);

#* USAGE:
#*     $0 [options] input.cif [input2.cif ...]
#* 
#* OPTIONS:
#*     --use-perl-parser
#*        Use Perl parser for CIF parsing.
#*
#*     --use-c-parser
#*        Use Perl & C parser for CIF parsing.
#*
#*     --debug, --no-debug
#*        Turn on/off the debug prints of reduction algorithm. Default off.
#*
#*     --help,--usage
#*        Print short usage message (this message) and exit.
#**

@ARGV = getOptions
(
    "--debug"    => sub{ $COD::Cell::Niggli::KG76::debug = 1 },
    "--no-debug" => sub{ $COD::Cell::Niggli::KG76::debug = 0 },

    "--use-perl-parser" => sub{ $use_parser = "perl" },
    "--use-c-parser"    => sub{ $use_parser = "c" },

    "--help,--usage" => sub { usage; exit },
);

@ARGV = ( "-" ) unless @ARGV;

for my $filename (@ARGV) {

    my( $data, $error_count );

    if( $use_parser eq "perl" ) {
        my $parser = new COD::CIFParser::CIFParser;

        $data = $parser->Run($filename);

        if( defined $parser->YYData->{ERRCOUNT} ) {
            $error_count = $parser->YYData->{ERRCOUNT};
        }
    } else {
        require COD::CIF::Parser::Bison;
        ( $data, $error_count ) = COD::CIF::Parser::Bison::parse($filename);
    }

    canonicalize_all_names( $data );

    for my $datablock (@$data) {
        my $values = $datablock->{values};
        my @cell = get_cell( $values, $filename, $datablock->{name} );
        my $f2o = symop_ortho_from_fract( @cell );
        my @niggli = reduce( @cell );
        my $o2f = symop_fract_from_ortho( @niggli );
        my $CoB = pop( @niggli );

        $values->{_cell_length_a}[0] = $niggli[0];
        $values->{_cell_length_b}[0] = $niggli[1];
        $values->{_cell_length_c}[0] = $niggli[2];
        $values->{_cell_angle_alpha}[0] = $niggli[3];
        $values->{_cell_angle_beta}[0]  = $niggli[4];
        $values->{_cell_angle_gamma}[0] = $niggli[5];

        my( $a, $b, $c ) = @cell;
        my( $ca, $cb, $cg ) = map { cos( $Pi * $_ / 180 ) }
                                  @cell[3..5];

        for my $i (0..$#{$values->{_atom_site_label}}) {
            my( $fx, $fy, $fz ) = map { s/(\(\d+\))?$//; $_ }
                                  map { $values->{$_}[$i] }
                                    qw( _atom_site_fract_x
                                        _atom_site_fract_y
                                        _atom_site_fract_z );

            my( $x, $y, $z ) = ( $a*$fx, $b*$fy, $c*$fz );

            my $M = [
                [     $x*$x, $x*$y*$cg, $x*$z*$cb ],
                [ $x*$y*$cg,     $y*$y, $y*$z*$ca ],
                [ $x*$z*$cb, $y*$z*$ca,     $z*$z ],
            ];

            my $Gprime =
                symop_mul( symop_mul( symop_transpose( $CoB ), $M ), $CoB );

            my $new_coordinates = [ sqrt($Gprime->[0][0])/$niggli[0],
                                    sqrt($Gprime->[1][1])/$niggli[1],
                                    sqrt($Gprime->[2][2])/$niggli[2] ];

            $values->{_atom_site_fract_x}[$i] = $new_coordinates->[0];
            $values->{_atom_site_fract_y}[$i] = $new_coordinates->[1];
            $values->{_atom_site_fract_z}[$i] = $new_coordinates->[2];
        }

        print_cif( $datablock,
           {
                preserve_loop_order => 1,
                keep_tag_order => 1
           }
        );
    }
}

sub mat3_inv
{
    my( $m ) = @_;
    my $c = 1 / symop_det($m);
    return [[ $c * ( $m->[1][1] * $m->[2][2] - $m->[1][2] * $m->[2][1] ),
             -$c * ( $m->[0][1] * $m->[2][2] - $m->[0][2] * $m->[2][1] ),
              $c * ( $m->[0][1] * $m->[1][2] - $m->[0][2] * $m->[1][1] )],
            [-$c * ( $m->[1][0] * $m->[2][2] - $m->[1][2] * $m->[2][0] ),
              $c * ( $m->[0][0] * $m->[2][2] - $m->[0][2] * $m->[2][0] ),
             -$c * ( $m->[0][0] * $m->[1][2] - $m->[0][2] * $m->[1][0] )],
            [ $c * ( $m->[1][0] * $m->[2][1] - $m->[1][1] * $m->[2][0] ),
             -$c * ( $m->[0][0] * $m->[2][1] - $m->[0][1] * $m->[2][0] ),
              $c * ( $m->[0][0] * $m->[1][1] - $m->[0][1] * $m->[1][0] )]];
}

sub cholesky($)
{
    my( $m ) = @_;
    return [[0,0,0],[0,0,0],[0,0,0]] if $m->[0][0] == 0;

    my $a = sqrt($m->[0][0]);
    my $b = 0;
    my $c = 0;
    my $d = $m->[1][0] / $a;
    my $g = $m->[2][0] / $a;

    my $e = sqrt($m->[1][1] - $d**2);
    my $f = 0;
    my $h = ($m->[2][1] - $d*$g) / $e;

    my $i = sqrt($m->[2][2] - $g**2 - $h**2);
    return [[$a,$b,$c],[$d,$e,$f],[$g,$h,$i]];
}

#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Generate a list of all tags in a CIF dictionary.
#**
use strict;
use warnings;
use COD::CIF::Parser qw( parse_cif );
use COD::SOptions;
use COD::SUsage;

my $use_parser = 'c';

#* USAGE:
#*     $0 --options input1.cif input*.cif
#*
#* OPTIONS:
#*
#* --use-perl-parser
#*                     Use development CIF parser written in Perl.
#* --use-c-parser
#*                     Use faster C/Yacc CIF parser (default).
#*
#* --help,--usage      Print a short usage message (this message) and exit.
#**
@ARGV = getOptions(
    "--use-perl-parser" => sub{ $use_parser = "perl" },
    "--use-c-parser"    => sub{ $use_parser = "c" },

    "--help,--usage"    => sub { usage; exit }
);

@ARGV = ("-") unless @ARGV;

foreach my $filename (@ARGV) {
    my ( $data ) = parse_cif( $filename, { parser => $use_parser } );

    for my $dataset (@$data) {

        my $values = $dataset->{values};
        if( defined $values && defined $values->{_name} ) {
            foreach my $tag ( @{$values->{_name}} ) {
                my $category = $values->{_category}[0];
                if ( defined $category && $category ne 'category_overview' ) {
                    print "$tag\n";
                };
            };
        }

        my $save_blocks = $dataset->{save_blocks};
        if( defined $save_blocks ) {
            for my $save_block (@{$save_blocks}) {
                my $values = $save_block->{values};

                if( defined $values && defined $values->{"_item.name"} ) {

                    my @name = @{$values->{"_item.name"}};

                    for my $name (@name) {
                        print "$name\n";
                    }
                }
            }
        }

    }
}

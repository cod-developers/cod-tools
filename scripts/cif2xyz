#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Read CIF files and print atom coordinates in XYZ format.
#**

use strict;
use warnings;
use COD::CIF::Parser qw( parse_cif );
use COD::AtomProperties;
use COD::CIF::Data qw( get_cell );
use COD::Fractional qw( symop_ortho_from_fract );
use COD::Spacegroups::Symop::Algebra qw( symop_vector_mul );
use COD::SOptions;
use COD::SUsage qw( usage options );
use COD::UserMessage qw( warning error prefix_dataname );

my $print_radii = 0;
my $use_parser = "c"; # Used CIF parser

#* USAGE:
#*     $0 --options input.cif
#*
#* OPTIONS:
#*     --print-radii
#*         Append covalent radii to the atom coordinates.
#*
#*     --no-print-radii, --dont-print-radii, --xyz-only
#*         Print only Cartesian XYZ coordinates for input atoms
#*         (default behaviour)
#*
#*     --use-perl-parser
#*     --use-c-parser
#*         Specify parser to parse CIF files. C parser is default.
#*
#*     --help, --usage
#*         Print a short usage message (this message) and exit.
#**
@ARGV = getOptions(
    "--print-radii" => sub{ $print_radii = 1 },
    "--no-print-radii,--dont-print-radii" => sub{ $print_radii = 0 },
    "--xyz-only" => sub{ $print_radii = 0 },

    "--use-perl-parser" => sub{ $use_parser = "perl" },
    "--use-c-parser"    => sub{ $use_parser = "c" },

    "--options"      => sub { options; exit },
    "--help,--usage" => sub { usage; exit }
);

@ARGV = ("-") unless @ARGV;

for my $filename (@ARGV) {

    my $options = { 'parser' => $use_parser, 'no_print' => 1 };
    my ( $data, $err_count, $messages ) = parse_cif( $filename, $options );

    if ( $err_count > 0 ) {
        print STDERR $_ foreach ( @$messages );
        error( $0, $filename, undef, "$err_count error(s) "
             . "encountered while parsing the file", undef );
        exit(255);
    }
    print STDERR $_ foreach ( @$messages );

    if( !@{$data} || !defined $data->[0] || !defined $data->[0]{name} ) {
        warning( $0, $filename, undef, "file seems to be empty", undef );
        next;
    }

    for my $dataset (@$data) {
        my $values = $dataset->{values};
        my $dataname = prefix_dataname($dataset->{name});

        my @cell = get_cell( $values, $filename, $dataset->{name} );

        my $f2o = symop_ortho_from_fract(@cell);
        # perform fractional to orthogonal conversion here

        my $atom_site_tag;

        if( exists $values->{"_atom_site_label"} ) {
            $atom_site_tag = "_atom_site_label";
        } elsif( exists $values->{"_atom_site_type_symbol"} ) {
            $atom_site_tag = "_atom_site_type_symbol";

            error( $0, $filename, $dataname,
                   '_atom_site_label tag was not found',
                   'a serial number will be appended to ' .
                   '_atom_site_type_symbol to make atom labels' );
        } else {
            error( $0, $filename, $dataname, 'neither _atom_site_type_symbol '
                 . 'nor _atom_site_label were found', undef );
            return undef;
        }

        my $atom_labels = $values->{$atom_site_tag};

        for (my $i = 0; $i < @{$atom_labels}; $i++) {
            my $covalent_radius;
            if( $print_radii ) {
                my $atom_type;
                if( exists $values->{_atom_site_type_symbol} &&
                    defined $values->{_atom_site_type_symbol}[$i] &&
                    $values->{_atom_site_type_symbol}[$i] ne '?' ) {
                    $atom_type = $values->{_atom_site_type_symbol}[$i];
                    if( $atom_type =~ m/^([A-Za-z]{1,2})/ ) {
                        $atom_type = ucfirst( lc( $1 ));
                    }
                } else {
                    if( $values->{_atom_site_label}[$i] =~
                        m/^([A-Za-z]{1,2})/ ) {
                        $atom_type = ucfirst( lc( $1 ));
                    } else {
                        die( "could not determine atom type for atom " .
                             "'$values->{_atom_site_label}[$i]'" );
                    }
                }
                if( !exists $COD::AtomProperties::atoms{$atom_type} ) {
                    die( "unknown atom type: '$atom_type'" );
                }
                $covalent_radius =
                    $COD::AtomProperties::atoms{$atom_type}->{covalent_radius};
            }

            my @atom_xyz;
            for my $cif_fract ( "_atom_site_fract_x",
                                "_atom_site_fract_y",
                                "_atom_site_fract_z",)
            {
                push( @atom_xyz, $values->{$cif_fract}[$i] );
                $atom_xyz[-1] =~ s/\(\d+\)$//;
            }
            my @coordinates_ortho = symop_vector_mul( $f2o, \@atom_xyz );

            print $coordinates_ortho[0], " ",
                  $coordinates_ortho[1], " ",
                  $coordinates_ortho[2], " ",
                  ($print_radii ? "$covalent_radius " : ""),
                  '# ', $atom_labels->[$i], "\n";
        }
    }
}

#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Classify a CIF structure -- find out if it is organic compound,
#  inorganic, mineral, etc.
#**

use strict;
use File::Basename;
use COD::CIFParser::CIFParser;
use COD::CIF::Data::CIFClassifyer;
use COD::AtomProperties;
use COD::UserMessage;
use COD::SOptions;
use COD::SUsage;

my $use_parser = "c";

#* USAGE:
#*   $0 [options] input1.cif input*.cif
#* 
#* OPTIONS:
#*   --use-perl-parser  Use development CIF parser written in Perl
#*   --use-c-parser     Use faster C/Yacc CIF parser (default)
#*
#*   --help,--usage     Print a short usage message (this message) and exit.
#**

@ARGV = getOptions( 
    "--use-perl-parser"  => sub{ $use_parser = "perl" },
    "--use-c-parser"     => sub{ $use_parser = "c" },

    "--help,--usage"     => sub { usage; exit },
);

@ARGV = ( "-" ) unless @ARGV;

for my $filename (@ARGV) {

    my( $data, $error_count );

    if( $use_parser eq "perl" ) {
        my $parser = new COD::CIFParser::CIFParser;

        $data = $parser->Run( $filename );

        if( defined $parser->YYData->{ERRCOUNT} ) {
            $error_count = $parser->YYData->{ERRCOUNT};
        }
    } else {
        require COD::CIF::Parser::Bison::Bison;
        ( $data, $error_count ) = COD::CIF::Parser::Bison::Bison::parse( $filename );
    }

    if( defined $error_count && $error_count > 0 ) {
        print STDERR "$0: ", $error_count,
        " error(s) encountered while parsing file '${filename}'\n";
        next;
    }

    if( !@$data || !defined $data->[0] || !defined $data->[0]{name} ) {
        warn( "file '$filename' seems to be empty" );
        next;
    }

    my $bond_safety_margin = 0.2; # Angstroems

    for my $datablock (@$data) {
        eval {
            my $flags = cif_class_flags
                ( $datablock, $filename, \%COD::AtomProperties::atoms,
                  $bond_safety_margin );

            if( $flags ) {
                print $datablock->{name}, "\t", $flags, "\n";
            } else {
                print $datablock->{name}, "\t", 0, "\n";
            }
        };
        if( $@ ) {
            warn "$0: $filename $datablock->{name}: ", lcfirst($@)
        }
    }
}
